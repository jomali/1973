

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 1: Habitación de Gabriel
!!
!!==============================================================================
!!
!!	File:			1973_escena1.inf
!!	Author(s):		J. Francisco Martín (jfm.lisaso@gmail.com)
!!	Languague:		ES (Castellano)
!!	System:			Inform, INFSP 6
!!	Platform:		Z-Machine / GLULX
!!	Version:		0.0
!!	Released:		0000/00/00
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2013, J. Francisco Martín
!!
!!	This file is part of PROYECTO 1973.
!!
!!	PROYECTO 1973 is free software: you can redistribute it and/or modify 
!!	it under the terms of the GNU General Public License as published by 
!!	the Free Software Foundation, either version 3 of the License, or 
!!	(at your option) any later version.
!!
!!	PROYECTO 1973 is distributed in the hope that it will be useful, 
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of 
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License 
!!	along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	HABITACION: En la habitación
!!------------------------------------------------------------------------------
!!	1. Tocar TRACK_1_EXPOSITION, sin repeticiones, con FLAG_SOUND_NOTIFY:
!!		Damusix.AsignarCanal(TRACK_1_EXPOSITION, 0, -1, 1);
!!		Damusix.Tocar(TRACK_1_EXPOSITION, 1);
!!	2. Tocar TRACK_1_ACTION, con repeticiones:
!!		Damusix.AsignarCanal(TRACK_1_ACTION, 0, -1, -1);
!!		Damusix.Tocar(TRACK_1_ACTION, 1);
!!	3. Cambiar repeticiones de TRACK_1_ACTION):
!!		Damusix.Repeticion(TRACK_1_ACTION, 1);
!!	4. Tocar TRACK_1_DENOUEMENT, sin repeticiones, con FLAG_SOUND_NOTIFY:
!!		Damusix.AsignarCanal(TRACK_1_DENOUEMENT, 0, -1, 1);
!!		Damusix.Tocar(TRACK_1_DENOUEMENT, 1);

Room	HABITACION "En la habitación"
 with
		name	'habitacion' 'cuarto' 'dormitorio' 'estancia', 
		short_name " ", 
		initial [; if (self hasnt visited) {
			!! Inicializaciones multimedia: ------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(WOUNDED_A, 0, -1, 1);
			Damusix.AsignarCanal(WOUNDED_B, 1, -1, -1);
			Damusix.AsignarCanal(WOUNDED_C, 2, -1, 1);
			Damusix.Tocar(WOUNDED_A, 1);
			next_track = WOUNDED_B;
			#Endif; ! TARGET_GLULX;
			!! Otra inicializaciones de la escena: -----------------------------
			SetGrammaticalInflection(PRESENTE_TERCERA_PERSONA);
			if (flowers == nothing) SelectFlowerType();
			!! Introducción ----------------------------------------------------
			box	""
				" Te ha dolido, ¿verdad?, dijo el chico.           "
				" Si.                                              "
				" ¿Eres muy valiente?                              "
				" Regular.                                         "
				" ¿Qué es lo más valiente que has hecho?           "
				" Escupió en la carretera una flema sanguinolenta. "
				" Levantarme esta mañana, dijo.                    "
				""
				"                                   CORMAC MCCARTY "
				"                                   The Road, 2006 "
				"";
			PressAnyKey();
			EraseWindow();
			box	""
				" Te ha dolido, ¿verdad?, dijo el chico.           "
				" Si.                                              "
				" ¿Eres muy valiente?                              "
				" Regular.                                         "
				" ¿Qué es lo más valiente que has hecho?           "
				" Escupió en la carretera una flema sanguinolenta. "
				" Levantarme esta mañana, dijo.                    "
				""
				"                                   CORMAC MCCARTY "
				"                                   The Road, 2006 "
				"";
			print (parser) "Pulsa una tecla para empezar";
			PressAnyKey();
			EraseWindow();
print "No debió ser así. Eso es lo primero que el joven Gabriel Mistral piensa al abrir los ojos y despertar en la sofocante oscuridad de la habitación. Aún permanece unos minutos en la cama, tumbado con la mirada perdida entre las sombras del techo mientras las imágenes de los últimos días desfilan ante él en un tropel confuso. Una y otra vez ha repasado esas imágenes, obstinado, convencido de que había algo oculto en ellas que no podía ver, una clave secreta que necesitaba desentrañar para comprender lo ocurrido. Pero una y otra vez se ha encontrado incapaz de dar con ello. ¿Cómo se podía haber torcido todo de esa manera? ¿Dónde se habían equivocado?^";
			new_line;
print "Vuelve a cerrar los ojos un momento y entonces el perfume de ", (the) flowers, " lo ocupa todo. Ella. Automáticamente tiende la mano a un lado, buscando a ciegas el tacto de su cuerpo, pero lo único que encuentra son las sábanas, húmedas y pegajosas. No, claro que no está, recuerda. No aquí. La dejó durmiendo en el hotel antes de volverse a casa en mitad de la noche, solo de nuevo. Decide que es mejor así, al fin y al cabo ahora tiene cosas más importantes de las que preocuparse. De modo que deja escapar un largo suspiro de resignación y se obliga a incorporarse finalmente en el colchón.^";
			PressAnyKey();
			new_line;
			self.short_name = "En la habitación";
			DrawStatusLine();
			return true;
		}],
		description [;
			if (~~self.is_dark)
				"La intensa claridad del amanecer se cuela ahora por la ventana 
				abierta, iluminando un caos de ropa, libros, papeles y otros 
				trastos esparcidos por todo el dormitorio. Sólo su cama y el 
				escritorio siguen aún en pie entre el tumulto.";
			else if (self.described)
				"Apenas una fina franja de luz logra colarse en la habitación 
				por entre los resquicios que dejan las cortinas cerradas. 
				Insuficiente todavía como para descorrer el telón de tinieblas 
				que lo envuelve todo.";
			else {
				self.described = true;
				"A su alrededor todo son penumbras. La escasa luz capaz de 
				filtrarse a través de las cortinas cerradas sirve tan sólo para 
				dibujar una estrecha franja blanca y zigzagueante sobre el 
				suelo de la habitación.";
			}
		], 
		before [;
			Listen:
				print "Desde el exterior llega el eco";
				if (self.is_dark) print " distante y apagado";
				" de motores.";
			Smell:
				if (~~self.is_dark)
					"El aire que se cuela por la ventana es fresco y fragante.";
				else if (self.smelled)
					"Huele al sudor empapado entre las sábanas.";
				else {
					self.smelled = true;
					"Durante unos instantes permanece suspendido entre las 
					tinieblas el perfume de ", (the) flowers, ". Sin embargo 
					está sólo en su memoria y se desvanece enseguida. Aquí y 
					ahora el único aroma que hay es el acre del sudor que 
					empapa las sábanas.";
				}
		], 
		e_to [;
!! TODO: comprobaciones
!! XXX
print "Gabriel se detiene a echar un último vistazo a la habitación y tuerce la boca en una media sonrisa al pensar que se parece mucho a su propia vida; puesta toda patas arriba, llena de sueños rotos, pisoteados y tirados por el suelo. Al cabo de un momento, sin más, le da la espalda al pasado y sale con paso decidido por la puerta abierta.^";
			PressAnyKey();
			new_line;
			PlayerTo(CAFE);	! FIN DE LA ESCENA
			return true;
		],
		out_to [;
			return self.e_to();
		],
		cant_go
			"No puede ir por ahí. La puerta es la única salida de la 
			habitación.", 
		is_dark	true,
		inhibit_object_list true, 
 private
		!! Indica si el objeto ha sido ya descrito al usuario o no
		described false, 
		!! Indica si ha sido olido o no
		smelled false, 
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!! TODO: nombres y adjetivos
Atrezzo	"puerta" HABITACION
 with
		name_f	'puerta' 'entrada' 'salida', 
		gender	G_FEMENINO, 
		adjectives 'madera' 'pintada' 'blanco' 'blanca' 'sencilla' 'cerrada' 
			'abierta', 
		description [;
			if (self has open) "Está abierta de par en par.";
			"De madera pintada en blanco. Ahora que está cerrada deja ver el 
			colorido calendario de pared que tiene colgado por detrás.";
		], 
		after [;
			Close:
				"La puerta se cierra con un ligero crujir de goznes.";
			Open:
				"La puerta se abre con un ligero crujir de goznes.";
		], 
 has
		female openable ~open transparent;

!! TODO: nombres y adjectivos
Atrezzo	-> -> "calendario de pared"
 with	name_m	'calendario' 'anuario', 
		gender	G_MASCULINO, 
		adjectives 'pared' 'colorido', 
		description [;
			"En su mitad superior hay una copia a color de una pintura de Van 
			Gogh: un bodegón floral de tonos cálidos en el que varios claveles 
			anaranjados comparten jarra con otros blanquecinos de tallo más 
			largo. La mitad inferior del calendario está ocupada por la 
			cuadrícula ordenada de los días de septiembre y al final, en 
			grandes números: ", (emph) "1973", ".";
		], 
		before [;
			Examine:
				if (parent(self) has open) {
					give parent(self) ~open;
					print "Cierra la puerta para ver mejor el calendario que 
					está colgado detrás.^";
					new_line;
				}
			Close, Open:
				"Ya está bien como está.";
			Pull, Push, PushDir, Take:
				"Está enganchado a la puerta.";
		], 
 has	male;

!!------------------------------------------------------------------------------
!! NPCs:

!!------------------------------------------------------------------------------
!! Items:

Item	maleta "maleta de viaje" HABITACION
 with
		name_m	'equipaje' 'maleton', 
		name_f	'maleta',
		gender	G_FEMENINO, 
		adjectives 'viaje' 'cuero' 'gran' 'grande' 'desgastado' 'desgastada', 
		invent [;
			if (inventory_stage == 1 && self in HABITACION) {
				print "su maleta de viaje, ya preparada y apoyada junto a la 
				puerta";
				return true;
			}
		],
		describe [;
			if (self in TAXI)
				"^La maleta reposa sobre el asiento de al lado.";
		],
		description [;
			print "Es un maletón de viaje forrado en cuero desgastado. Dentro 
			lleva lo poco que guardará de la que ha sido su vida hasta ahora; 
			algo de ropa y un par de libros. Nada más.";
			if (~~self.described) {
				self.described = true;
				" Ningún recuerdo. Esos tendrá que dejárselos atrás.";
			}
			new_line;
			return true;
		],
		before [;
			Close:
				"Ya está cerrada.";
			Open, Receive:
				if (location == HABITACION)
					"Ya tiene dentro todo cuanto puede llevar consigo.";
				else
					"Ahora no necesita nada de lo que lleva dentro de la 
					maleta.";
		],
		after [; 
			Take: 
				if (self hasnt moved) 
					"Recoge la maleta del suelo."; 
		],
		size 5, 
 private
		!! Indica si el objeto ha sido ya descrito al usuario o no
		described false, 
 has
		female openable;

!!------------------------------------------------------------------------------
!! Furniture:

Furniture	escritorio "escritorio" HABITACION
 with	name_m	'mueble' 'mobiliario' 'moblaje' 'escritorio' 'pupitre',
		name_mp	'muebles',
		name_f	'mesa',
		gender	G_MASCULINO,
		adjectives 'madera' 'pequeno' 'pequena' 'oscuro' 'oscura',
		description [;
			print "Se trata de un funcional escritorio de madera oscura";
			if (child(self)) {
				print " sobre el que hay ";
				WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT);
			}
			print ". Tiene un único cajón, que en estos momentos está";
			if (cajon hasnt open) " cerrado.";
			print " abierto.";
			if (~~child(cajon)) " Dentro no hay nada.";
			print " Dentro alcanza a ver ";
			WriteListFrom(child(cajon), CONCEAL_BIT + ENGLISH_BIT);
			".";
		], 
		before [;
			Climb, Enter, Sit:
				"En realidad no tiene motivos para subirse ahí.";
			Close:
				<<Close cajon>>;
			Open:
				<<Open cajon>>;
			Pull, Push, PushDir, Take, Turn:
				print_ret (The) self, " pesa", (n) self, " demasiado para andar 
				moviéndol", (o) self, ".";
		], 
 has	male concealed supporter transparent;

Furniture	cajon "cajón" HABITACION
 class	Receptacle, 
 with	capacity_size 3,
		name_m	'cajon' 'cajoncillo' 'compartimento', 
		name_f	'gaveta',
		gender	G_MASCULINO, 
		adjectives 'escritorio', 
		description [; <<Search self>>; ], 
		before [;
			Pull: 
				<<Open self>>;
			Push:
				<<Close self>>;
			PushDir, Take, Turn:
				"Forma parte del escritorio.";
		],
 has	male concealed container openable ~open;

!!------------------------------------------------------------------------------
!! Atrezzo:


