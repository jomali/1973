

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 3: Calles de Santiago
!!
!!==============================================================================
!!
!!	File:			1973_escena3.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/02/01
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!	Este archivo forma parte de PROYECTO 1973.
!!
!!	PROYECTO 1973 es software libre: usted puede redistribuirlo y/o 
!!	modificarlo bajo los términos de la Licencia Pública General GNU 
!!	publicada por la Fundación para el Software Libre, ya sea la versión 
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	PROYECTO 1973 se distribuye con la esperanza de que sea útil, pero 
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o 
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de 
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU 
!!	junto a PROYECTO 1973. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	TAXI: Dentro del taxi
!!------------------------------------------------------------------------------

!! TODO
!!	<>	definir asientos
!!	<>	radio
!!	<>	abrir/cerrar puerta
!!	<>	acción gritar

Room	obj_TAXI "Dentro del taxi"
 with	name	'taxi' 'coche' 'vehiculo' 'automovil' 'auto', 
		initial [; if (self hasnt visited) {
			!! Inicializaciones multimedia: ------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(SATIATE_PERCUSSION, 0, 0, -1);
			Damusix.AsignarCanal(SATIATE_STRINGS, 1, 10, -1);
			Damusix.AsignarCanal(TRAFFIC_ENVIRONMENT, 2, 50, -1);
			Damusix.AsignarCanal(STREET_ENVIRONMENT, 3, 50, -1);
			Damusix.Tocar(SATIATE_PERCUSSION);
			Damusix.Tocar(SATIATE_STRINGS);
			Damusix.Tocar(TRAFFIC_ENVIRONMENT);
			#Endif; ! TARGET_GLULX;
			!! Otra inicializaciones de la escena: -----------------------------
			SetGrammaticalInflection(THIRD_PERSON_PRESENT);
			move obj_maleta to self;
			StartTimer(self, 5); !! TODO: ajustar temporizador
			!! Introducción ----------------------------------------------------
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			if (self.state == 2) return O__M(self, 3);
			else return O__M(self, 2);
		],
		before [;
			!! TODO Jump:
			Listen:
				"En la radio suenan las pausadas notas de piano de alguna 
				pieza clásica que no puede reconocer.";
			Look:
				if (self.trigger)
					return self.next_state();
			!! TODO: Sit
			Smell:
				if (self.state == 2) "El olor del cigarro ocupa el vehículo.";
				else "Humo viejo, viejo perfume, viejo sudor.";
			!! TODO StandUp:
		],
		time_left, 
		time_out [;
			self.trigger = true;
			StartDaemon(self);
		],
		daemon [;
			if (action ~= ##Examine) {
				new_line;
				self.next_state();
			}
		], 
		e_to [;
			if (self.state == 0 || self.state == 1) {
				"No puede abandonar el taxi en marcha.";
			}
			if (self.state == 2) {
				self.state = 3;
				O__M(self, 4);
				new_line;
			}
			#Ifdef TARGET_GLULX;
			Damusix.Volumen(TRAFFIC_ENVIRONMENT, 0);
			!Damusix.Tocar(STREET_ENVIRONMENT);
			Damusix.FadeIn(SATIATE_PERCUSSION, 1000, 50);
			#Endif; ! TARGET_GLULX;
			give obj_puerta_taxi open;
			return obj_puerta_taxi;
		],
		out_to [;
			return self.e_to();
		],
		get_state [;
			return self.state;
		],
 private
		!! Indicador del estado en que se encuentra el taxi:
		!!	0.	estado inicial
		!!	1.	tras pasar por Neftalí Basoalto
		!!	2.	recién detenido en el atasco
		!!	3.	listo para reanudar la marcha
		state 0,
		!! Indica si el taxi está preparado para avanzar a la etapa siguiente
		trigger false,
		!! Avanza al siguiente estado del objeto
		next_state [;
			self.trigger = false;
			StopDaemon(self);
			if (self.state == 0)
				return self.crosses_neftali_basoalto();
			if (self.state == 1)
				return self.gets_stuck();
		],
		!! Avanza al estado: 1
		crosses_neftali_basoalto [ flag;
			#Ifdef TARGET_GLULX;
			Damusix.FadeIn(SATIATE_STRINGS, 500, 20);
			#Endif; ! TARGET_GLULX;
!! TODO: ajustar temporizador
			StartTimer(self, 5);
			self.state = 1;
			print "El coche da una ligera sacudida de pronto al cambiar de 
			calle. Gabriel advierte que acaban de doblar por Neftalí Basoalto, 
			y al momento le vienen a la mente los Juegos Florales que aquí se 
			celebraron hace unos meses. Él participó, pues siempre ha adorado 
			la poesía. Se presentó con un breve poemario: ~Luces de 
			Medianoche~, del que recuerda unos versos en especial:^";
			new_line;
			print (em) "@<<Las mareas susurran al incierto / oscuro cielo 
			estrellado, / lamentos y lloros amargos / grandes duelos y sueños 
			aciagos. / Pues no conocen de mayor acierto / ni peor sino marcado, 
			/ el fin de la noche amante, / de su rostro fresco, siempre 
			radiante. / Con el amanecer a cielo abierto / el Sol brilla 
			asombrado, / de ver su noche desnuda / a los pies ya muerta y 
			siempre muda.@>>^";
			new_line;
			print "---Eso es muy bonito, ¿es suyo?^";
			flag = PressAnyKey(flag);
			new_line;
			print "Siente cómo la sangre le salta repentinamente a la cara; no 
			se había dado cuenta de que recitaba en voz alta.^";
			new_line;
			ConversationManager.start(obj_conversacion_taxista_1);
			return true;
		],
		!! Avanza al estado: 2
		gets_stuck [ flag;
			#Ifdef TARGET_GLULX;
			Damusix.FadeIn(SATIATE_STRINGS, 500, 50);
			#Endif; ! TARGET_GLULX;
			self.state = 2;
			print "Ven cómo se iluminan las luces de freno de los coches que 
			tienen delante y acto seguido ellos mismos tienen que frenar hasta 
			quedar completamente detenidos.^";
			new_line;
			print "El taxista suspira largamente y gira levemente la cabeza 
			hacia Gabriel.^";
			new_line;
			print "---¿Tiene mucha prisa";
			if (obj_taxista.k_poetry) print ", señor poeta";
			print "?^";
			flag = PressAnyKey(flag);
			new_line;
			print "---Necesito estar en San Antonio para eso de las cuatro.^";
			flag = PressAnyKey(flag);
			new_line;
			print "Los ojos del hombre se desvían hacia la maleta durante un 
			instante antes de volver a fijarse en los de Gabriel. Su tono de 
			voz desenfadado suena serio de pronto:^";
			new_line;
			print "---Va a huir del país, ¿eh?^";
			new_line;
			ConversationManager.start(obj_conversacion_taxista_2);
			return true;
		],
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

NPC		obj_taxista "taxista" obj_TAXI
 with
		name_m	'taxista' 'conductor' 'chofer' 'hombre' 'individuo', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! TODO: adjetivos
		description [;
			print "Se trata de un hombre de mediana edad, con los brazos 
			desnudos bronceados por el sol. Mordisquea el extremo de un largo 
			cigarro apagado mientras observa concentrado el estado del tráfico";
			if (~~self.described) {
				self.described = true;
				", como un jugador de ajedrez experimentado que, erguido sobre 
				el tablero, estudia la disposición de las fichas antes de 
				ejecutar su siguiente movimiento.";
			}
			".";
		],
		before [;
			TalkTo:
				if (~~ConversationManager.is_running())
					"El taxista permanece en silencio, ensimismado en la 
					carretera y en sus pensamientos. Gabriel decide no 
					interrumpirle y centrarse en los suyos propios.";
				switch (self.last_topic) {
				0:
					print "---Eso que ha recitado, ---insiste el conductor, 
					---¿lo ha escrito usted?^";
					new_line;
				default:
					print "El taxista le mira atento a través del 
					retrovisor.^";
					new_line;
				}
				ConversationManager.show_topic_inventory();
				return true;
		],
		last_topic 0,	 	! Identificador del último tema discutido
		k_poetry false,		! Sabe que Gabriel escribe poesía
 private
		!! Indica si el objeto ha sido ya descrito al usuario o no
		described false, 
 has	male concealed;

!!------------------------------------------------------------------------------
!! Conversación con el taxista:

Conversation obj_conversacion_taxista_1
 with	topics obj_op03 obj_op02 obj_taxista_tema_01;

ConversationTopic obj_taxista_tema_01
 class	TextSequence
 with	description
			"---Es... es algo que escribí yo."
			"---¡No me diga! De modo que es usted poeta, ¿eh?"
			"---Supongo que si. De los malos, me temo. No conseguí ningún 
			premio."
			"---¿Cómo dice?"
		[;
			print "---Eso que he recitado. Son unos versos que presenté a un 
			concurso. Aquí al lado, hace unos meses. No gané nada. ---Aunque sí 
			se sintió bastante satisfecho con su obra.";
			if (obj_ventanilla.is_described())
				print " Juzgaba que había mejorado mucho desde los pareados 
				grabados a punta de navaja en un árbol.";
		]
			"---Lástima. Bueno, ya habrá mejor suerte la próxima vez."
			"Duda mucho que vaya a haber una próxima vez. Hasta ahora estaba 
			seguro de que si abandonaba Chile sería para regresar algún día, 
			más tarde o más temprano, cuando todo fuese seguro de nuevo. Pero, 
			¿y si Pablo tenía razón? ¿Qué sentido tenía volver? No deja atrás 
			más que recuerdos, y no hay forma de regresar al pasado. ¿Por qué 
			motivo iba a volver aquí?"
			"Afortunadamente, en esta ocasión tiene cuidado de guardarse esos 
			pensamientos para sí mismo y no expresarlos en voz alta.",
		entry [;
			print (strong) "responder", " a la pregunta";
		],
		keys
			'responder,' 'responderle,' 'responde,' 'pregunta',
		reply [;
			self.display();
		],
		reaction [;
			obj_taxista.last_topic = 1;
			obj_taxista.k_poetry = true;
		];

ConversationTopic obj_op02
 with	entry [;
			print "hablar sobre el ", (strong) "estado del tráfico";
		],
		keys	'estado' 'trafico' 'coches' 'atasco' 'carretera', 
		reply [ flag;
			print "---No es habitual que se forme un atasco así a estas horas, 
			¿no es cierto?";
			if (obj_taxista.last_topic == 0 && ~~obj_taxista.k_poetry)
				print " ---cambia de tema Gabriel.";
			new_line;
			flag = PressAnyKey(flag);
			new_line;
			print "---No, no es habitual. ¿Pero qué hay de habitual 
			últimamente? Ocurre que han cortado un par de calles allí atrás y 
			han reconducido todo ese tráfico por aquí. No sé cuándo demonios 
			terminará de volver todo a la normalidad. Todos todos los puñeteros 
			días montan estos pifostios de... ---deja la frase en el aire, 
			incapaz tal vez de dar con una palabra lo suficientemente 
			malsonante.^";
		], 
		reaction [;
			parent(self).remove_topic(obj_taxista_tema_01);
			obj_taxista.last_topic = 2;
			self.append_topic_inventory = true;
		], 
;

ConversationTopic obj_op03
 with	entry [;
			print "hablar sobre el ", (strong) "golpe contra el gobierno";
		],
		keys	'golpe' 'contra' 'gobierno' 'estado' 'sublevacion' 'militar', 
		reply [ flag;
			print "---¿Puedo preguntarle una cosa?";
			if (obj_taxista.last_topic == 0 && ~~obj_taxista.k_poetry)
				print " ---cambia de tema Gabriel.";
			new_line;
			flag = PressAnyKey(flag);
			new_line;
			print "---Claro. Adelante.^";
			flag = PressAnyKey(flag);
			new_line;
			print "---¿Qué opinión tiene de todo lo que ha ocurrido? Ya sabe, 
			el... eh... la caída de Allende y... y todo eso.^";
			flag = PressAnyKey(flag);
			new_line;
			print "---Qué quiere que le diga. Yo no meto el hocico donde no me 
			llaman, ¿sabe? Además, ¿qué sé yo? Sólo soy un taxista. Yo entiendo 
			de motores, de en qué lugares suele haber clientes necesitando que 
			les lleven y de esas cosas. Nada más. Prefiero dejar la política 
			para quienes sepan de política, si me comprende usted.^";
			flag = PressAnyKey(flag);
			new_line;
			print "---Si, desde luego.^";
		], 
		reaction [;
			parent(self).remove_topic(obj_taxista_tema_01);
			obj_taxista.last_topic = 3;
			self.append_topic_inventory = true;
		], 
;

Conversation obj_conversacion_taxista_2
 with	topics obj_op06 obj_op05 obj_op04;

ConversationTopic obj_op04
 with	keys	'ser,' 'se,' 'contestar,' 'contesta,' 'sincero' 'sinceridad', 
		entry [;
			print (strong) "ser sincero";
		],
		reply [;
			print "---Si. Voy a escapar de Chile en barco.";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(obj_op05);
			parent(self).remove_topic(obj_op06);
			obj_taxista.last_topic = 4;
		],
		subtopics obj_op02 obj_op03;

ConversationTopic obj_op05
 with	keys	'mentir,' 'miente,' 'mentira', 
		entry [;
			print (strong) "mentir";
		],
		reply [;
			print "---¿Que si voy a huir? ¡Qué va! Qué va... vaya tontería...";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(obj_op04);
			parent(self).remove_topic(obj_op06);
			obj_taxista.last_topic = 5;
		],
		subtopics obj_op02 obj_op03;

ConversationTopic obj_op06
 with	keys	'guardar,' 'guarda,' 'silencio', 
		entry [;
			print (strong) "guardar silencio";
		], 
		reply [;
print "Al ver que Gabriel no contesta, el taxista se inclina a un lado para abrir la guantera y descubre un pequeño alijo de estuches de cerillas en el interior del receptáculo. Coge uno, y utiliza la llama de un fósforo para encenderse el cigarro.";
			new_line;
			new_line;
print "---Bueno, ---rompe finalmente el silencio, y exhalando una gran nube de humo contra la luna delantera del coche. ---sea como sea no se preocupe. Tardaremos un rato en pasar esto, pero hay tiempo más que de sobra para llegar hasta San Antonio a hacer lo que sea que tenga que hacer allí.";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(obj_op04);
			parent(self).remove_topic(obj_op05);
			obj_taxista.last_topic = 6;
		],
		subtopics obj_op02 obj_op03;

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:

!! TODO
Atrezzo	"ciudad" obj_TAXI
 with	name_m	'santiago' 'edificio' 'parque' 'parquecillo' 'paseo' 'bulevar' 
				'bordillo' 'anden' 'pavimento', 
		name_f	'ciudad' 'urbe' 'capital' 'metropoli' 'calle' 'carretera' 
				'acera' 'manzana' 'via' 'avenida' 'rambla' 'ronda' 'calzada', 
		name_mp	'edificios' 'parques' 'calles', 
		name_fp	'aceras', 
		gender	G_FEMENINO, 
		before [;
			Examine:
				<<Examine obj_ventanilla>>;
		], 
 has	female;

!! TODO
Atrezzo	obj_puerta_taxi "puerta" obj_TAXI
 with	name_f	'puerta', 
		gender	G_FEMENINO, 
		before [;
			Examine:
				<<Examine obj_ventanilla>>;
		],
		door_dir 
			out_to, 
		door_to	
			obj_CALLE_ARRIBA,
 has	female door openable ~open;

!! TODO:
!!	<>	abrir / cerrar ventanilla
Atrezzo	obj_ventanilla "ventanilla" obj_TAXI
 with
		name_m	'cristal' 'vidrio', 
		name_f	'ventanilla' 'ventana' 'luna', 
		name_mp	'cristales' 'vidrios', 
		name_fp	'ventanillas' 'ventanas', 
		gender	G_FEMENINO, 
		adjectives 'lateral' 'laterales' 'puerta' 'puertas', 
		description [; 
			if (obj_TAXI.get_state() == 3) {
!! XXX
				"Descripción de la calle con el coche parado.";
			} else if (obj_TAXI.get_state() == 2) {
				<<Go out_obj>>;
			} else if (~~self.described) {
				self.described = true;
				"Al mirar por la ventanilla reconoce un parquecillo con árboles 
				a lo lejos. En otra época, cuando eran poco más que niños, 
				recuerda grabar rudimentarias declaraciones de amor sobre las 
				cortezas de aquellos árboles. Aunque a él, en realidad, los 
				corazones asaeteados y los pareados empalagosos nunca le 
				funcionaron para ligar.";
			} else {
				"Pierde la mirada entre los detalles de la ciudad que se 
				desvanece tras las ventanillas, sin fijarse realmente en nada.";
			}
		],
		before [;
			Search: 
				<<Examine self>>; 
		], 
		is_described [;
			return self.described;
		], 
 private
		!! Si está activado, se imprime una descripción reducida del objeto
		described false, 
 has
		female;


!!==============================================================================
!!	CALLE_ARRIBA: Santiago
!!------------------------------------------------------------------------------

Room	obj_CALLE_ARRIBA "Santiago"
 with	name	'calle' 'calles' 'santiago' 'aceras' 'acera' 'via' 'paseo' 
				'avenida' 'bulevar' 'rambla' 'ronda' 'bordillo' 'anden' 
				'pavimento' 'calzada' 'carretera',
		description [;
!! XXX
print "Apenas una semana después del golpe, el ambiente que se respira en la calle vuelve a ser el de siempre, como si nunca hubiera ocurrido nada. Los habitantes de Santiago han vuelto a sus quehaceres diarios. Las tiendas y los bares están frecuentados otra vez por la clientela habitual, los ociosos pasean tranquilamente bajo el sol del mediodía y todos se esfuerzan, en fin, por recuperar la vida que tenían antes. Al menos aquéllos que aún conservan una.";
			new_line;
		],
		s_to	obj_CALLE_ABAJO, 
		w_to	obj_taxi_aparcado, 
		in_to	[; return self.w_to(); ],
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:

Atrezzo	obj_taxi_aparcado "taxi" obj_CALLE_ARRIBA
 with	name_m	'taxi' 'coche' 'vehiculo' 'automovil' 'auto', 
		gender	G_MASCULINO, 
		adjectives 'parado' 'detenido' 'aparcado', 
		door_dir 
			in_to, 
		door_to
			obj_TAXI, 
 has	male door openable ~open;


!!==============================================================================
!!	CALLE_ABAJO: Santiago
!!------------------------------------------------------------------------------

Room	obj_CALLE_ABAJO "Santiago"
 with	name	'calle' 'calles' 'santiago' 'aceras' 'acera' 'via' 'paseo' 
				'avenida' 'bulevar' 'rambla' 'ronda' 'bordillo' 'anden' 
				'pavimento' 'calzada' 'carretera',
		description [;
!! XXX:
print "Un puñado de pasos calle abajo la ve por fin. Esta a punto de echar a correr a su encuentro, pero se detiene en el último momento. Luz está en una terraza cercana, pero no es ella la única a quién ve. Dos soldados están sentados a una de las mesas y la han hecho detenerse. Tras unos instantes interrogándola acerca de algún asunto ---Gabriel no puede escuchar cuál---, la dejan continuar. Luz se sienta a otra mesa de la terraza, sola.";
			new_line;
		],
		n_to	obj_CALLE_ARRIBA,
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:


