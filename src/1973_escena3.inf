

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 3: Calles de Santiago
!!
!!==============================================================================
!!
!!	File:			1973_escena3.inf
!!	Author(s):		J. Francisco Martín (jfm.lisaso@gmail.com)
!!	Languague:		ES (Castellano)
!!	System:			Inform, INFSP 6
!!	Platform:		Z-Machine / GLULX
!!	Version:		0.0
!!	Released:		0000/00/00
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2013, J. Francisco Martín
!!
!!	This file is part of PROYECTO 1973.
!!
!!	PROYECTO 1973 is free software: you can redistribute it and/or modify 
!!	it under the terms of the GNU General Public License as published by 
!!	the Free Software Foundation, either version 3 of the License, or 
!!	(at your option) any later version.
!!
!!	PROYECTO 1973 is distributed in the hope that it will be useful, 
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of 
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License 
!!	along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	TAXI: Dentro del taxi
!!------------------------------------------------------------------------------

!! TODO
!!	<>	definir asientos
!!	<>	radio

Room	TAXI "Dentro del taxi"
 private
		!! Indicador del estado en que se encuentra el taxi:
		!!	0.	estado inicial
		!!	1.	tras pasar por Neftalí Basoalto
		!!	2.	recién detenido en el atasco
		!!	3.	listo para reanudar la marcha
		state 0,
		!! Indica si el taxi está preparado para avanzar a la etapa siguiente
		trigger false,
		crosses_neftali_basoalto [;
!! TODO: ajustar temporizador
			StartTimer(self, 5);
			self.state = 1;
			self.trigger = false;
!! XXX
print "El coche da una ligera sacudida al cambiar de calle. Gabriel advierte que acaban de doblar por Neftalí Basoalto, y al momento le vienen a la mente los Juegos Florales que aquí se celebraron hace unos meses. Él participó, pues siempre ha adorado la poesía. Se presentó con un breve poemario: ~Luces de Medianoche~, del que recuerda unos versos en especial:";
			new_line;
			new_line;
print (em) "@<<Las mareas susurran al incierto / oscuro cielo estrellado, / lamentos y lloros amargos / grandes duelos y sueños aciagos. / Pues no conocen de mayor acierto / ni peor sino marcado, / el fin de la noche amante, / de su rostro fresco, siempre radiante. / Con el amanecer a cielo abierto / el Sol brilla asombrado, / de ver su noche desnuda / a los pies ya muerta y siempre muda.@>>";
			new_line;
			new_line;
			print "---Eso es muy bonito, ¿es suyo?";
			new_line;
			new_line;
			print "Siente cómo la sangre le salta de repente a la cara. No se 
			había dado cuenta de que recitaba en voz alta.";
			new_line;
			new_line;
			ConversationManager.start(conversacion_taxista_1);
			return true;
		],
		gets_stuck [;
			self.state = 2;
!! XXX
print "Gabriel ve cómo se iluminan las luces de freno del coche que tienen delante y un instante después ellos mismos empiezan a frenar hasta detenerse por completo.";
			new_line;
			new_line;
			print "---¿Va todo bien?";
			new_line;
			new_line;
!! XXX
print "---En este país no va bien una mierda últimamente. Han cortado una calle un poco más atrás y todo ese tráfico pasa ahora por aquí. Así que se montan unos pifostios de... ---deja la frase en el aire, tal vez porque no es capaz de encontrar una palabra lo suficientemente malsonante. Después gira la cabeza hacia atrás, mirando a Gabriel por encima del hombro ---¿tiene mucha prisa";
			if (taxista.poeta) print ", señor poeta";
			print "?";
			new_line;
			new_line;
			print "---Tengo que estar en San Antonio sobre las 16:15. Mejor 
			antes.";
			new_line;
			new_line;
			print "---De modo que está huyendo, ¿eh?";
			new_line;
			new_line;
			ConversationManager.start(conversacion_taxista_2);
			return true;
		],
 with
		name	'taxi' 'coche' 'vehiculo' 'automovil' 'auto', 
		initial [; if (self hasnt visited) {
			SetGrammaticalInflection(PRESENTE_TERCERA_PERSONA);
			move maleta to self;
!! TODO: ajustar temporizador
			StartTimer(self, 5);
print "Siente en la mano el frío peso del revólver un instante antes de comprender que lleva un rato jugueteando con él, absorto. Inmediatamente lo vuelve a guardar en uno de los bolsillos del abrigo y mira alarmado al taxista; éste, por suerte, tiene toda su atención puesta en la carretera y no parece prestar demasiada atención a nada más. No da señas de haber reparado en el arma, al menos. Con el susto aún en el cuerpo, Gabriel respira hondo y se recuesta mejor en el asiento, posando la mirada sobre la ciudad que se va esfumando poco a poco tras las ventanillas. Intentando alejar los recuerdos de su cabeza.";
			new_line;
			new_line;
			return true;
		}],
		description [;
			if (self.state == 2)
!! XXX
				"El humo blanquecino del cigarro flota pesadamente en el 
				interior del vehículo. Están detenidos en mitad de un atasco. Y 
				detenida también ha quedado la ciudad tras las ventanillas, 
				como una última intantánea para el recuerdo.";
			else
!! XXX
				"El taxi avanza lentamente a través de las congestionadas 
				calles de Santiago. Desde su asiento en la parte de atrás, 
				Gabriel ve cómo desfilan, quizá por última vez, las calles, los 
				edificios y las gentes de la que ha sido su ciudad hasta hoy.";
		],
		before [;
			Listen:
				"En la radio suenan las pausadas notas de piano de alguna 
				pieza clásica que no puede reconocer.";
			Look:
				if (self.trigger && self.state == 0)
					return self.crosses_neftali_basoalto();
				if (self.trigger && self.state == 1)
					return self.gets_stuck();
			Smell:
!! XXX
				if (self.state == 2) "El olor del cigarro ocupa el vehículo.";
				else "Humo viejo, viejo perfume, viejo sudor.";
		],
		time_left, 
		time_out [;
			self.trigger = true;
		],
		e_to [;
			if (self.state == 0 || self.state == 1) {
!! XXX
				"No puede abandonar el taxi en marcha.";
			}
			if (self.state == 2) {
				self.state = 3;
				print "Distraído, posa los ojos sobre la gente que pasea por la 
				acera esta mañana. Y de repente, durante apenas un instante, 
				vuelve a su mente el intenso perfume de ", (the) flowers 
				,". ¡Ella! Allí, a tan sólo un puñado de metros, ve a Luz 
				Acosta caminando sinuosa por la calle. Alta, cabello negro 
				largo, vestido rojo, piernas rectas que castigan el suelo a 
				cada paso y una mirada capaz de derribar montañas. Luz 
				Acosta... la mujer que conoció anoche.";
				new_line;
				new_line;
				print "---¡Pare! ¡Pare! ¡Espere aquí un momento! ---Abre 
				apresuradamente la puerta del taxi y sale a su encuentro, casi 
				a la carrera. Perfectamente consciente del tremendo riesgo que 
				podía conllevar ser visto.";
				new_line;
				new_line;
			}
			give puerta_taxi open;
			return puerta_taxi;
		],
		out_to [;
			return self.e_to();
		],
		get_state [;
			return self.state;
		],
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

NPC		taxista "taxista" TAXI
 private
		!! Indica si el objeto ha sido ya descrito al usuario o no
		described false, 
 with
		name_m	'taxista' 'conductor' 'chofer' 'hombre' 'individuo', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! TODO: adjetivos
		description [;
			print "Se trata de un hombre de mediana edad, con los brazos 
			desnudos bronceados por el sol. Mordisquea el extremo de un largo 
			puro apagado mientras observa concentrado el estado del tráfico";
			if (~~self.described) {
				self.described = true;
				", como un experimentado jugador de ajedrez que, erguido sobre 
				el tablero, estudia la disposición de las fichas antes de 
				ejecutar su siguiente movimiento.";
			}
			".";
		],
		before [;
			TalkTo:
!! TODO: estudiar implementación
				if (ConversationManager.current_conversation == 0)
					"El taxista permanece en silencio, absorto en la carretera 
					y en sus pensamientos. Gabriel decide no interrumpirle y 
					centrarse en los suyos propios.";
				switch (self.estado) {
				0:
					print "---Eso que ha recitado, ---insiste el conductor, 
					---¿lo ha escrito usted?";
					new_line;
					new_line;
				default:
					print "El taxista le hace un leve gesto con la cabeza, como 
					haciéndole ver que le está escuchando.";
					new_line;
					new_line;
				}
				ConversationManager.show_topic_inventory();
				return true;
		],
		estado 0,		! estado del taxista
		poeta false,	! indica si el taxista sabe que Gabriel escribe poesía
 has
		male concealed;

TI_Conversation	conversacion_taxista_1
 with	topics op03 op02 op01;

TI_Topic	op01
 with	id	1,
		entry [;
			print (strong) "responder", (em) " a la pregunta";
			italic_style();
		],
		keys	'responder,' 'reponderle,' 'responde,' 'pregunta',
		reply [;
!! XXX
			print "---Si, es algo que escribí yo. Me alegro que le guste.^";
			new_line;
			print "---Vaya, así que es usted poeta.^";
			new_line;
			print "---Supongo. De los malos. No gané el premio.^";
			new_line;
			print "---¿Cómo dice?^";
			new_line;
			print "---Presenté esos versos a un concurso. Aquí al lado, hace 
			unos meses. No conseguí el premio. ---Aunque sí se sintió bastante 
			satisfecho de su obra.^";
			new_line;
			print "---Lástima. Bueno, la próxima vez será.^";
			new_line;
			print "Lo duda mucho. No habrá una próxima vez, se dice. Pero en 
			esta ocasión tiene cuidado de guardárselo para si mismo y no 
			decirlo en voz alta.^";
		], 
		reaction [;
			taxista.estado = self.id;
			taxista.poeta = true;
			self.append_topic_inventory = true;
		],
;

TI_Topic	op02
 with	id		2,
		entry [;
			print (em) "hablar del ", (strong) "estado del tráfico";
			italic_style();
		],
		keys	'estado' 'trafico' 'coches' 'atasco' 'carretera', 
		reply [;
			print "---No es habitual que se forme un atasco así a estas horas, 
			¿no es cierto?";
			if (taxista.estado == 0 && ~~taxista.poeta)
				print " ---cambia de tema Gabriel.";
			new_line;
			new_line;
			print "---Nada de lo que ocurre estos días es habitual. Todavía 
			queda alguna calle cortada, y los camiones del ejército andan de un 
			lado para otra parando el tráfico, así que últimamente hay atascos 
			a todas horas.";
			new_line;
			new_line;
			print "---Parando el tráfico... ¿controles? ---no puede evitar que 
			la voz se le escape con una octava de más.";
			new_line;
			new_line;
			"---Si. El otro día me hicieron parar un par de veces. Pero no. No 
			he vuelto a ver un control desde entonces. Lo que pasa es que 
			aparece un camión y entonces te tienen ahí quieto hasta que termina 
			de pasar. Como si toda la puñetera ciudad fuera su casa. Enseguida 
			vuelven a abrir la carretera. En cuanto termina de pasar, claro. 
			Para cuando arrancas ya se ha montado el pifostio.";
		],
		reaction [;
			parent(self).remove_topic(op01);
			taxista.estado = self.id;
		],
		append_topic_inventory true,
;

TI_Topic	op03
 with	id		3,
		entry [;
			print (em) "hablar sobre el ", (strong) "golpe contra el gobierno";
			italic_style();
		],
		keys	'golpe' 'contra' 'gobierno' 'estado' 'sublevacion' 'militar', 
		reply [;
!! TODO
			print "---¿Puedo preguntarle una cosa? ¿Qué le parece todo lo que 
			ha ocurrido? Ya sabe, la caída de Allende y todo eso...";
			if (taxista.estado == 0 && ~~taxista.poeta)
				print " ---cambia de tema Gabriel.";
			new_line;
			new_line;
			"---Puede preguntar, desde luego. Pero no espere que le conteste. 
			Yo no meto el hocico donde no me llaman. Yo sólo sé de mi taxi y de 
			cuáles son los sitios dónde la gente suele necesitarme. Y ya.";
		], 
		reaction [;
			parent(self).remove_topic(op01);
			taxista.estado = self.id;
			give self ~visited;
		], 
		append_topic_inventory true,
;

TI_Conversation	conversacion_taxista_2
 with	topics op06 op05 op04;

TI_Topic	op04
 with	id	4, 
		keys	'ser,' 'se,' 'contestar,' 'contesta,' 'sincero' 'sinceridad', 
		entry [;
			print (strong) "ser sincero";
			italic_style();
		],
		reply [;
			print "---Si. Voy a escapar de Chile en barco.";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(op05);
			parent(self).remove_topic(op06);
		],
		subtopics op02 op03;

TI_Topic	op05
 with	id	5,
		keys	'mentir,' 'miente,' 'mentira', 
		entry [;
			print (strong) "mentir";
			italic_style();
		],
		reply [;
			print "---¿Que si voy a huir? ¡Qué va! Qué va... vaya tontería...";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(op04);
			parent(self).remove_topic(op06);
		],
		subtopics op02 op03;

TI_Topic	op06
 with	id	6, 
		keys	'guardar,' 'guarda,' 'silencio', 
		entry [;
			print (strong) "guardar silencio";
			italic_style();
		], 
		reply [;
print "Al ver que Gabriel no contesta, el taxista se inclina a un lado para abrir la guantera y descubre un pequeño alijo de estuches de cerillas en el interior del receptáculo. Coge uno, y utiliza la llama de un fósforo para encenderse el cigarro.";
			new_line;
			new_line;
print "---Bueno, ---rompe finalmente el silencio, y exhalando una gran nube de humo contra la luna delantera del coche. ---sea como sea no se preocupe. Tardaremos un rato en pasar esto, pero hay tiempo más que de sobra para llegar hasta San Antonio a hacer lo que sea que tenga que hacer allí.";
			new_line;
		],
		reaction [;
			parent(self).remove_topic(op04);
			parent(self).remove_topic(op05);
		],
		subtopics op02 op03;

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:

!! TODO
Atrezzo	"ciudad" TAXI
 with	name_m	'santiago' 'edificio' 'parque' 'parquecillo' 'paseo' 'bulevar' 
				'bordillo' 'anden' 'pavimento', 
		name_f	'ciudad' 'urbe' 'capital' 'metropoli' 'calle' 'carretera' 
				'acera' 'manzana' 'via' 'avenida' 'rambla' 'ronda' 'calzada', 
		name_mp	'edificios' 'parques' 'calles', 
		name_fp	'aceras', 
		gender	G_FEMENINO, 
		before [;
			Examine:
				<<Examine ventanilla>>;
		], 
 has	female;

!! TODO
Atrezzo	puerta_taxi "puerta" TAXI
 with	name_f	'puerta', 
		gender	G_FEMENINO, 
		before [;
			Examine:
				<<Examine ventanilla>>;
		],
		door_dir 
			out_to, 
		door_to	
			CALLE_ARRIBA,
 has	female door openable ~open;

!! TODO:
!!	<>	abrir / cerrar ventanilla
Atrezzo	ventanilla "ventanilla" TAXI
 private
		!! Si está activado, se imprime una descripción reducida del objeto
		described false, 
 with	counter 0, 
		name_m	'cristal' 'vidrio', 
		name_f	'ventanilla' 'ventana' 'luna', 
		name_mp	'cristales' 'vidrios', 
		name_fp	'ventanillas' 'ventanas', 
		gender	G_FEMENINO, 
		adjectives 'lateral' 'laterales' 'puerta' 'puertas', 
		description [; 
			if (TAXI.get_state() == 2)
				<<Go out_obj>>;
			else if (~~self.described) {
				self.described = true;
				"Al mirar por la ventanilla reconoce un parquecillo con árboles 
				a lo lejos. En otra época, cuando eran poco más que niños, 
				recuerda utilizar aquellos troncos para grabar primerizas 
				declaraciones de amor adolescente con ayuda de alguna pequeña 
				navaja. Aunque las muchachas nunca encontraron aquellos 
				garabatos especialmente románticos. A él, ciertamente, nunca le 
				funcionaron.";
			}
			else
				"Su mirada se pierde entre los detalles de la ciudad que se va 
				desvaneciendo poco a poco tras las ventanillas.";
		],
		before [;
			Search: 
				<<Examine self>>; 
		], 
 has	female;


!!==============================================================================
!!	CALLE_ARRIBA: Santiago
!!------------------------------------------------------------------------------

Room	CALLE_ARRIBA "Santiago"
 with	name	'calle' 'calles' 'santiago' 'aceras' 'acera' 'via' 'paseo' 
				'avenida' 'bulevar' 'rambla' 'ronda' 'bordillo' 'anden' 
				'pavimento' 'calzada' 'carretera',
		description [;
!! XXX
print "Apenas una semana después del golpe, el ambiente que se respira en la calle vuelve a ser el de siempre, como si nunca hubiera ocurrido nada. Los habitantes de Santiago han vuelto a sus quehaceres diarios. Las tiendas y los bares están frecuentados otra vez por la clientela habitual, los ociosos pasean tranquilamente bajo el sol del mediodía y todos se esfuerzan, en fin, por recuperar la vida que tenían antes. Al menos aquéllos que aún conservan una.";
			new_line;
		],
		s_to	CALLE_ABAJO, 
		w_to	taxi_aparcado, 
		in_to	[; return self.w_to(); ],
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:

Atrezzo	taxi_aparcado "taxi" CALLE_ARRIBA
 with	name_m	'taxi' 'coche' 'vehiculo' 'automovil' 'auto', 
		gender	G_MASCULINO, 
		adjectives 'parado' 'detenido' 'aparcado', 
		door_dir 
			in_to, 
		door_to
			TAXI, 
 has	male door openable ~open;


!!==============================================================================
!!	CALLE_ABAJO: Santiago
!!------------------------------------------------------------------------------

Room	CALLE_ABAJO "Santiago"
 with	name	'calle' 'calles' 'santiago' 'aceras' 'acera' 'via' 'paseo' 
				'avenida' 'bulevar' 'rambla' 'ronda' 'bordillo' 'anden' 
				'pavimento' 'calzada' 'carretera',
		description [;
!! XXX:
print "Un puñado de pasos calle abajo la ve por fin. Esta a punto de echar a correr a su encuentro, pero se detiene en el último momento. Luz está en una terraza cercana, pero no es ella la única a quién ve. Dos soldados están sentados a una de las mesas y la han hecho detenerse. Tras unos instantes interrogándola acerca de algún asunto ---Gabriel no puede escuchar cuál---, la dejan continuar. Luz se sienta a otra mesa de la terraza, sola.";
			new_line;
		],
		n_to	CALLE_ARRIBA,
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!------------------------------------------------------------------------------
!! NPCs:

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Atrezzo:


