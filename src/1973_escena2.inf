

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 2: Café Blanca Florida
!!
!!==============================================================================
!!
!!	File:			1973_escena2.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/02/11
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!	Este archivo forma parte de PROYECTO 1973.
!!
!!	PROYECTO 1973 es software libre: usted puede redistribuirlo y/o 
!!	modificarlo bajo los términos de la Licencia Pública General GNU 
!!	publicada por la Fundación para el Software Libre, ya sea la versión 
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	PROYECTO 1973 se distribuye con la esperanza de que sea útil, pero 
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o 
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de 
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU 
!!	junto a PROYECTO 1973. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------
!!
!!	# NOTAS
!!
!!	General:
!!	 *	FIXME - Llevar a cabo todas las inicializaciones de la escena. Además 
!!		de modificar la flexión gramatical de los mensajes de la librería, el 
!!		inventario del protagonista en esta escena es diferente del que tenía 
!!		en la escena anterior.
!!	 *	FIXME - Revisar la lógica del sistema de ayuda cuando el jugador no 
!!		empieza el relato desde la escena 1.
!!	 *	TODO - Cuando el protagonista coja el billete y el revólver que le da 
!!		Pablo, se pone fin a la ESCENA 2 y se pasa a la siguiente. Como con 
!!		toda acción clave para el desarrollo de la historia, antes de coger los 
!!		objetos se deben mostrar signos de duda en el protagonista.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	CAFE: Café Blanca Florida
!!------------------------------------------------------------------------------

Room	OBJ_CAFE "Café Blanca Florida"
 with	name	'cafe' 'blanca' 'florida' 'cafeteria' 'bar' 'local' 'taberna' 
				'meson' 'tasca' 'establecimiento',
		initial [; if (self hasnt visited) {
			!! Inicializaciones de la escena -----------------------------------
			!! TODO - Resto de inicializaciones
			SetGrammaticalInflection(THIRD_PERSON_PAST);
			!! Inicializaciones multimedia -------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(BAR_ENVIRONMENT, 0, 50, -1);
			Damusix.Tocar(BAR_ENVIRONMENT);
			#Endif; ! TARGET_GLULX;
			!! Introducción ----------------------------------------------------
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 2, (self has visited));
		],
		w_to	[; return self.out_to(); ],
		out_to	"No podía irse aún. No sin hablar antes con Pablo.",
		cant_go "Por ahí no podía ir a ningún lado.";

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_silla_cafe "silla" OBJ_CAFE
 class	SmartEnterable
 with	name_m	'asiento' 'taburete', 
		name_f	'banqueta' 'silla', 
		gender	G_FEMENINO, 
		!! FIXME - adjectives
		clarification "sentado a una de las mesas", 
		reach_objects OBJ_mesa_cafe OBJ_Pablo OBJ_camarera,
		drop_on_object OBJ_mesa_cafe,
		description "Era una silla normal y corriente.", 
		react_before [;
			if (self.SmartEnterable::react_before()) return true;
			TalkTo:
				if ((parent(player) ~= self) && (noun == OBJ_Pablo)
				&& (OBJ_conversacion_Pablo.get_counter() <= 1)) {
					move player to self;
					print "Atravesó el local hasta sentarse frente a él.^";
					new_line;
					return false;
				}
				if ((parent(player) == self) && (noun == OBJ_camarera)) {
					move player to location;
				}
			Examine, Look:
				if (parent(player) == self && noun == OBJ_telefono)
					"Un teléfono público negro. Desde la silla no podía ver 
					mucho más.";
		],
		before [;
			Pull, Push, PushDir, Take, Turn:
				"Ya estaba", (n) self, " muy bien donde estaba", (n) self, 
				". No había ningún motivo para andar moviéndol", (o) self, ".";
			Sit:
				<<Enter self>>;
			StandUp:
				<<Exit self>>;
		],
		after [;
			Enter:
				"Se sentó en ", (the) self, ", frente a Pablo.";
			Exit:
				"Se levantó de ", (the) self, ".";
		],
 has	female supporter transparent;

!!------------------------------------------------------------------------------
!! Atrezzo:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	"mesas y sillas del café" OBJ_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble' 'taburete',
		name_mp	'asientos' 'muebles' 'taburetes',
		name_fp	'banquetas' 'mesas' 'sillas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 
			'corriente' 'corrientes' 'funcional' 'funcionales' 'normal' 
			'normales' 'ordinaria' 'ordinarias' 'ordinario' 'ordinarios' 
			'sencilla' 'sencillas' 'sencillo' 'sencillos' 'simple' 'simples',
		description
			"Había unas cuantas mesas y sillas de aspecto corriente 
			repartidas por todo el bar.",
		before [;
			Examine, Search, LookUnder:
				return false;
			Enter, Sit:
				<<Enter OBJ_silla_cafe>>;
			Exit, StandUp:
				<<Exit OBJ_silla_cafe>>;
			default:
				"No era necesario preocuparse por eso. Después de todo, no 
				tenía", (n) self, " nada de especial.";
		],
 has	female pluralname;

!!	 *	El atributo 'general' se activa después de que Mr. Panamá se haya ido.
!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_cliente "cliente" OBJ_CAFE
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama',
		description [;
			if (self hasnt general) O__M(self, 1);
			else O__M(self, 2);
			if (OBJ_clientes hasnt general) {
				print " ";
				<<Listen OBJ_clientes>>;
			}
			new_line;
			return true;
		],
		react_before [;
			if (noun ~= self) return false;
			Attack: <<Attack OBJ_clientes second>>;
			Listen: <<Listen OBJ_clientes second>>;
			Pull, Push: <<Pull OBJ_clientes second>>;
			TalkTo: <<TalkTo OBJ_clientes second>>;
		],
 has	male; !! FIXME - animate?

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_clientes "clientes" OBJ_CAFE
 with	name_f	'clientela' 'gente' 'persona',
		name_mp	'chicos' 'clientes' 'hombres' 'mozos' 'parroquianos' 'senores',
		name_fp	'chicas' 'mozas' 'mujeres' 'parroquinas' 'personas' 'senoras',
		gender	G_MASCULINO + G_PLURAL,
		!! FIXME - adjectives
		description [;
			O__M(self, 1);
			if (~~self.listen_flag) {
				print " ";
				<<Listen self>>;
			}
			new_line;
			return true;
		],
		react_before [;
			Listen:
				if (noun == nothing)
					<<Listen self>>;
		],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.listen_flag) self.listen_flag = true;
				return O__M(self, 2);
			TalkTo:
				return O__M(self, 3);
		],
 private
		!! Se activa al escuchar el objeto por primera vez.
		listen_flag false,
 has	animate male pluralname;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_humo "humo" OBJ_CAFE
 with	name_m	'cigarrillo' 'cigarro' 'humo' 'pitillo' 'puro' 'tabaco',
		name_f	'fumarada' 'humarada',
		name_mp	'cigarrillos' 'cigarros' 'pitillos' 'puros',
		gender	G_MASCULINO, 
		adjectives
			'apestado' 'corrompido' 'hediondo' 'infecto' 'maloliente' 
			'pestilente' 'volutas',
		description [;
			return O__M(self, 1);
		], 
		react_before [;
			Smell: 
				if (noun == nothing)
					<<Smell self>>;
		], 
		before [;
			Examine:
				return false;
			Smell:
				return O__M(self, 2);
			default:
				"No hacía falta que se preocupase por eso.";
		], 
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_mesa_cafe "mesa" OBJ_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble',
		name_f	'mesa',
		name_mp	'muebles',
		gender	G_FEMENINO,
		adjectives
			'corriente' 'funcional' 'normal' 'ordinaria' 'ordinario' 'pablo' 
			'sencilla' 'sencillo' 'simple',
		description [;
			print "La mesa de Pablo era una de las que estaban más al fondo en 
			el local";
			if (child(self)) {
				print " Encima había ";
				WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT);
			}
			".";
		],
		before [;
			Climb, Enter, Sit:
				"No había ningún motivo para subirse ahí.";
			Pull, Push, PushDir, Take, Turn:
				print_ret (The) self, " ya estaba", (n) self, " bien donde 
				estaba", (n) self, ". No había motivos para andar moviéndol", 
				(o) self, ".";
		],
 has	concealed female supporter transparent;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_telefono "teléfono público" OBJ_CAFE
 with	name_m	'auricular' 'cajetin' 'disco' 'microfono' 'microtelefono' 
				'teclado' 'telef' 'telefono' 'telf' 'visor',
		name_f	'cabina' 'inscripcion' 'placa' 'ruleta',
		gender	G_MASCULINO,
		adjectives
			'aparato' 'ficha' 'fichas' 'negra' 'negro' 'numerico' 'publica' 
			'publico' 'telefonica' 'telefonico',
		description [;
			return O__M(self, 1, self.tokens);
		],
		before [;
			Use, TalkOn:
				!! TODO - Contemplar opción de llamar por telf.
				"No tenía a nadie a quien llamar por teléfono en aquel 
				momento.";
		],
		after [;
			!! Se añade el tema del teléfono a la conversación con la camarera:
			OBJ_conversacion_camarera.add_topic(OBJ_camarera_tema_telefono);
		],
 private
		tokens 0,
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_ventilador "ventilador" OBJ_CAFE
 with	name_m	'ventilador', 
		name_f	'aspa', 
		name_fp	'aspas', 
		gender	G_FEMENINO, 
		adjectives 'encendido' 'movimiento' 'negra' 'negras' 'negro' 'techo', 
		description [; return O__M(self, 1); ],
		before [;
			Examine:
				return false;
			Listen:
				return O__M(self, 2);
			default:
				return O__M(self, 3);
		],
 has	male;

!!------------------------------------------------------------------------------
!! Furniture:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	OBJ_barra "barra" OBJ_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mostrador' 'mueble',
		name_f	'barra' 'repisa',
		name_mp 'muebles',
		gender	G_FEMENINO, 
		!! FIXME - adjectives
		describe [ i obj;
			if (child(self) == nothing) return true;
			objectloop(obj in self)
				if (obj hasnt concealed or scenery or static)
					i++;
			if (i == 0) return true;
			if ((i == 1) && (OBJ_cerveza_Gabriel in self))
				"^La cerveza de Gabriel estaba sobre la barra.";
			print "^Sobre la barra podía ver ";
			WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT + PARTINV_BIT);
			".";
		],
		description [ i obj;
			!! Se añade el tema del teléfono a la conversación con la camarera:
			OBJ_conversacion_camarera.add_topic(OBJ_camarera_tema_telefono);
			!! Descripción de la barra:
			O__M(self, 1);
			new_line;
			if (child(self) == nothing) return true;
			objectloop(obj in self)
				if (obj hasnt concealed or scenery or static)
					i++;
			if (i == 0) return true;
			if ((i == 1) && (OBJ_cerveza_Gabriel in self))
				"La cerveza de Gabriel estaba sobre la barra.";
			print "Sobre la barra podía ver ";
			WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT + PARTINV_BIT);
			".";
		], 
		before [;
			Pull, Push, PushDir, Turn:
				"Le hubiese sido imposible mover eso.";
			Take:
				"Le hubiese sido imposible coger eso.";
		],
		after [ i obj;
			LetGo, Receive:
				objectloop(obj in self)
					if (obj hasnt concealed or scenery or static)
						i++;
				if (i == 0) give self concealed;
				else give self ~concealed;
		], 
 has	concealed female supporter;

!!------------------------------------------------------------------------------
!! Items:

!!	 *	FIXME - Nombres y adjetivos
Item	OBJ_cerveza_Gabriel "cerveza de Gabriel" OBJ_LIMBO
 with	name_m	'botellin', 
		name_f	'bebida' 'botella' 'cerveza', 
		gender	G_FEMENINO,
		adjectives 'alcohol' 'alcoholica' 'gabriel',
		description [;
			print "Un botellín de cerveza";
			if (self.counter == 0) " vacío.";
			if (self.counter == 1) " al que apenas le quedaba un trago.";
			if (self.counter <  4) " medio vacío.";
			if (self.counter < 10) " que Gabriel tenía a medias.";
			" sin empezar.";
		],
		before [;
			Drink:
				if (self.counter == 0) "Ya estaba vacío.";
				self.counter--;
				if (self.counter == 0) "Apuró el último trago del botellín.";
				"Gabriel dio un trago de su botellín de cerveza.";
		],
 private
		counter 0, ! Indica la cantidad de bebida que queda
 has	female;

!!	 *	Su atributo *general* se activa cuando Pablo se acaba la bebida.
!!	 *	FIXME - Nombres y adjetivos
Item	OBJ_vaso_vacio "bebida de Pablo" OBJ_mesa_cafe
 with	name_m	'tinto' 'vaso' 'vino',
		name_f	'bebida',
		gender	G_MASCULINO,
		adjectives 'alcohol' 'alcoholica' 'pablo' 'vacio',
		description [;
			if (self has general)
				"Pablo jugueteaba con el vaso vacío mientras esperaba a que 
				Gabriel fuese a por otra bebida.";
			"Un vaso de vino tinto casi vacío.";
		],
		before [;
			Examine:
				return false;
			default:
				"No tenía nada especial por lo que mereciese la pena perder el 
				tiempo con ello.";
		],
 has	female;

!!	 *	FIXME - Nombres y adjetivos
Item	OBJ_vino_Pablo "vino de Pablo" OBJ_LIMBO
 with	name_m	'tinto' 'vaso' 'vino',
		name_f	'bebida',
		gender	G_MASCULINO,
		adjectives 'alcohol' 'alcoholica' 'pablo',
		description "A Pablo siempre le había gustado el tinto.",
		before [;
			Examine:
				return false;
			Drink, Pull, Push, PushDir, Take, Touch, Turn:
				"Esa era la bebida de Pablo.";
			default:
				"No tenía nada especial por lo que mereciese perder el tiempo 
				con eso.";
		],
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Item	OBJ_billete_cafe "billete" OBJ_LIMBO
 with	name_m	'billete' 'papel' 'pase', 
		gender	G_MASCULINO, 
		adjectives 'antonio' 'barcaza' 'pedazo' 'san' 'trozo',
 has	male;

!!------------------------------------------------------------------------------
!! Camarera:

!!	 *	FIXME - Nombres y adjetivos
NPC		OBJ_camarera "camarera" OBJ_CAFE
 with	name_m	'camarero' 'despensero',
		name_f	'camarera' 'chica' 'dama' 'despensera' 'joven' 'moza' 
				'muchacha' 'mujer' 'persona' 'senora' 'senorita',
		gender	G_FEMENINO,
		!! FIXME - adjectives
		description [; return O__M(self, 1); ],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para comenzar una pelea.";
			TalkTo:
				ConversationManager.start(OBJ_conversacion_camarera);
				return true;
		],
		conversation_flag false,
 has	female concealed;

Conversation OBJ_conversacion_camarera
 with	talker [; print "Hablas con ", (the) OBJ_camarera; ],
		initial_action [;
			self.add_topic(OBJ_camarera_tema_cerveza);
			if (child(self)) {
				self.add_temporal_topic(OBJ_camarera_tema_temporal);
			}
			print "Gabriel se acercó hasta la barra.^";
			new_line;
			print (string) random (
				"---¿Quieres alguna cosa?",
				"---¿Te sirvo alguna cosa?",
				"---¿Qué vas a tomar?",
				"---¿Qué tomas?",
				"---¿Qué te sirvo?",
			);
			new_line;
			return true;
		],
		inter_action
			"La camarera observaba a Gabriel desde el otro lado de la barra.",
		final_action
			"No llegó a cruzar ninguna otra palabra con ella. Era con Pablo 
			con quien necesitaba hablar.";

ConversationTopic OBJ_camarera_tema_temporal
 with	entry [; print "no pedir ", (strong) "nada"; ],
		!! FIXME - keys
		keys
			'cosa' 'nada' 'ninguna' 'pedir' 'pedirla' 'pedirte' 'pide' 'pidela' 
			'pidete',
		reply [;
			give self ~visited;
			"No quería nada en realidad. Dio las gracias a la mujer y se alejó 
			de nuevo de la barra.";
		],
		reaction [; ConversationManager.end(); ];

ConversationTopic OBJ_camarera_tema_cerveza
 with	entry [; print "pedir una ", (strong) "bebida", " para Gabriel"; ],
		!! FIXME - keys
		keys
			'bebida' 'cerveza' 'gabriel' 'pedir' 'pedirla' 'pedirte' 'pide' 
			'pidela' 'pidete' 'trago',
		reply
			"---¿Puede ponerme una cerveza, por favor?
			^^
			Un momento después la camarera le sirvió un botellín de cerveza.";

ConversationTopic OBJ_camarera_tema_bebida
 class	TextSequence
 with	entry [;
			if (OBJ_cerveza_Gabriel in OBJ_LIMBO)
				print "pedir ", (strong) "bebidas";
			else print "pedir una ", (strong) "bebida para Pablo";
		],
		!! FIXME - keys
		keys
			'pedir' 'pide' 'bebidas' 'bebida' 'pablo',
		description
		[;
			if (OBJ_cerveza_Gabriel in OBJ_LIMBO)
				"---Ponme una cerveza y un tinto, por favor.";
			"---Ponme un tinto, por favor.";
		]
			"---Claro."
		[ x;
			if (OBJ_cerveza_Gabriel in OBJ_LIMBO) x = true;
			print "La camarera le sirvió";
			if (x) print " las bebidas"; else print " la bebida";
			print " y después Gabriel volvió hasta la mesa con";
			if (x) " ellas."; else " ella.";
		]
			"---Ahí tienes, Pablo."
			"---Gracias, chico.",
		reply [; self.display(); ];

ConversationTopic OBJ_camarera_tema_telefono
 class	TextSequence
 with	description
			"---Perdona, ¿para usar el teléfono?"
			"---Si, te vendo yo las fichas, si quieres."
			"---Bien. ¿Puedes darme alguna, por favor?"
			"La camarera le puso unas fichas sobre el mostrador."
			"---Ahí tienes.",
		entry [;
print "preguntar por el ", (strong) "teléfono";
		],
		!! FIXME - keys
		keys
			'pedir' 'pide' 'ficha' 'fichas' 'telefono' 'telef' 'telf' 'tlf',
		reply [;
			self.display();
		],
		reaction [;
		];

!!------------------------------------------------------------------------------
!! Pablo:

!!	 *	FIXME - Nombres y adjetivos
NPC		OBJ_Pablo "Pablo" OBJ_CAFE
 with	name_m	'pablo' 'ureta' 'amigo' 'hombre', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! FIXME - adjectives
		describe [;
			new_line;
			if (~~self.describe_flag) {
				self.describe_flag = true;
				return O__M(self, 1, (parent(player) == OBJ_silla_cafe));
			}
			return O__M(self, 2, (parent(player) == OBJ_silla_cafe));
		],
		description [;
			return O__M(self, 3, (parent(player) == OBJ_silla_cafe));
		],
		before [;
			TalkTo:
				!! TODO - Añadir a Mr. Panamá al café
				ConversationManager.start(OBJ_conversacion_Pablo);
				return true;
		],
		gets_angry [ i;
			if (i == 0) self.disposition++;
			else self.disposition = self.disposition + i;
		], 
 private
		describe_flag false,	! indica si el objeto ha sido descrito
		disposition 0,			! disposición de Pablo hacia Gabriel
 has	male proper;

Conversation OBJ_conversacion_Pablo
 with	talker [; print "Hablas con ", (the) OBJ_Pablo; ],
		initial_action [;
			switch (self.counter) {
			0:	self.counter++;
				self.add_topic(OBJ_Pablo_tema_02);
				self.add_topic(OBJ_Pablo_tema_01);
				"---Ahí estás. Llegas tarde, chico.";
			1:	"---¿Qué, chico? Tarde, pero has venido. ¿Es que ahora no vas a 
				decir nada?";
			2:	PrintOrRun(self, inter_action);
				return true;
			default:
				PrintOrRun(self, inter_action);
				return true;
			}
		],
		inter_action [;
			switch (self.counter) {
			1:	"---¿Y bien? ¿Es que no vas a decir nada?";
			2:	"Pablo señaló el vaso vacío que tenía delante.
				^^
				---Hazme el favor y pídeme otro, ¿quieres?";
			default:
				"Pablo miraba paciente a Gabriel.";
			}
		],
		get_counter [; return self.counter; ],
		set_counter [ i; self.counter = i; ],
		increase_counter [ i;
			if (i == 0) self.counter++;
			else self.counter = self.counter + i;
		],
 private counter 0;

ConversationTopic OBJ_Pablo_tema_01
 class	TextSequence
 with	entry [;
print "pedirle ", (strong) "perdón";
		],
		!! FIXME - keys
		keys
'disculparse' 'disculparte' 'disculpa' 'disculpas' 'excusarse' 'excusarte' 'excusas' 'pedir' 'pedirle' 'pedirte' 'perdon' 'perdone' 'perdonarle' 'perdonarte' 'perdonase' 'pide' 'pidele',
		description
"---Siento llegar tarde. He tenido que dar un rodeo enorme para llegar hasta aquí. Hay soldados por toda la ciudad."
"---Lo sé, lo sé. No te disculpes. Ya no hay sitio seguro en toda la capital. Tal como están las cosas, es no más cuestión de tiempo que nos agarren esos mamones con pistola... sin Allende, todo está perdido."
"Terminó lo que le quedaba en el vaso y se quedó en silencio, con la boca torcida en una mueca extraña y la mirada perdida en algún lugar lejos de aquel bar. Finalmente volvió en si. Fijó los ojos, primero en el vaso vacío que sostenía en la mano, y luego en Gabriel."
"---Hazme un favor, chico. Pídeme otro, ¿quieres?",
		reply [; self.display(); ],
		reaction [;
			!! FIXME - Cambiar descripciones de Pablo.
			OBJ_conversacion_Pablo.set_counter(2);
			!! Se modifica la conversación con la camarera:
			OBJ_conversacion_camarera
				.remove_topic(OBJ_camarera_tema_cerveza, true);
			OBJ_conversacion_camarera.add_topic(OBJ_camarera_tema_bebida);
		],
		relatives OBJ_Pablo_tema_02,
		subtopics OBJ_Pablo_tema_03 OBJ_Pablo_tema_04;

ConversationTopic OBJ_Pablo_tema_02
 class	TextSequence
 with	entry [;
print "llamarle ", (strong) "loco", " por citarle allí en un momento como aquel";
		],
		!! FIXME - keys
		keys
'llama' 'llamale' 'llamar' 'llamarle' 'loco' 'citarle' 'momento' 'aquel',
		description
"---¿Es que estás loco, Pablo? ¿Cómo se te ha ocurrido citarme aquí justo ahora? La ciudad está llena de soldados. He tenido que dar un rodeo enorme para no cruzarme a nadie de camino."
"---¡Noticias frescas! Claro que hay soldados, la capital entera se ha convertido en un hormiguero, carajo. Es no más cuestión de tiempo que nos agarren esos mamones con pistola... sin Allende, todo está perdido."
"Terminó lo que le quedaba en el vaso y se quedó en silencio, con la boca torcida en una mueca extraña y la mirada perdida en algún lugar lejos de aquel bar. Finalmente volvió en si. Fijó los ojos, primero en el vaso vacío que sostenía en la mano, y luego en Gabriel."
"---Haz algo, chico. Pídeme otro, ¿quieres?",
		reply [; self.display(); ],
		reaction [;
			!! FIXME - Cambiar descripciones de Pablo.
			OBJ_Pablo.gets_angry();
			OBJ_conversacion_Pablo.set_counter(2);
			!! Se modifica la conversación con la camarera:
			OBJ_conversacion_camarera
				.remove_topic(OBJ_camarera_tema_cerveza, true);
			OBJ_conversacion_camarera.add_topic(OBJ_camarera_tema_bebida);
		],
		relatives OBJ_Pablo_tema_01,
		subtopics OBJ_Pablo_tema_03 OBJ_Pablo_tema_04;

ConversationTopic OBJ_Pablo_tema_03
 with	entry [; print "pedirle otro ", (strong) "vino"; ],
		!! FIXME - keys
		keys 'pide' 'pidele' 'pedir' 'pedirle' 'otro' 'vino' 'tinto' 'bebida',
		reply "---Muy bien. Espera un momento.",
		relatives OBJ_Pablo_tema_04;

ConversationTopic OBJ_Pablo_tema_04
 with	entry [; print "decirle que no debía seguir ", (strong) "bebiendo"; ],
		!! FIXME - keys
		keys 'di' 'dile' 'decir' 'decirle' 'debia' 'beber' 'bebiendo' 'mas',
		reply "---No creo que debas seguir bebiendo.",
		reaction [; OBJ_Pablo.gets_angry(); ],
		relatives OBJ_Pablo_tema_03;




ConversationTopic _Pablo_tema_03
 class	TextSequence
 with	description
			"Pablo se hizo hasta la barra para devolver el vaso vacío y pedir 
			un juego de dardos a la camarera.
			^^
			---Has oido lo que ha ocurrido, ¿verdad? ---Hablaba mientras se 
			colocaba frente a la diana y empezaba a lanzar dardos."
			"---Dicen que lo mataron, pero era demasiado orgulloso para dejarse 
			arrastrar. Los de arriba hablan de que se dio el viaje..."
			"---¡Qué sabrán los de arriba! ---Su dardo se clavó con bastante 
			más fuerza de la necesaria. ---Ellos jamás creyeron en Salvador. Ya 
			antes le dieron la patada, ahora no iban a hacer menos. No, él no 
			huiría, lo dejó bien claro. Estoy seguro de que al final ha pagado 
			la lealtad del pueblo, pero también la deslealtad de los cabrones 
			de Unidad, que le dejaron bien en la Moneda con el culo al aire. 
			---Hizo entonces una pausa y lanzó su último dardo. ---No sé ya si 
			lo mataron o se mató él, pero estoy seguro de que..."
			"---No lo digáis..."
			!! TODO - calcular puntos de Pablo
			"---... está ya criando malvas, chico, y eso no lo vamos a 
			solucionar ni tú ni to. Estamos bien jodidos. ---Arrancó los dardos 
			de la diana y se los tendió luego a Gabriel. ---Quince y diez y 
			doce. Treinta y siente puntos. Ten. Tu turno.",
		entry [;
			print (strong) "aceptar", " partida";
		],
		keys	!! TODO
			'aceptar' 'partida' 'juego',
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_Pablo_tema_04);
		];

ConversationTopic _Pablo_tema_04
 class	TextSequence
 with	description
			"---No creo que sea buena idea llamar la atención.",
		keys	!! TODO
			'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "decirle que ", (strong) "no es buena idea", " llamar la 
			atención";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_Pablo_tema_03);
		],
		subtopics _Pablo_tema_05 _Pablo_tema_06;

ConversationTopic _Pablo_tema_05
 class	TextSequence
 with	description
			"---Vale. Echemos una partida.",
		keys	!! TODO
			'ceder' 'aceptar' 'partida' 'juego',
		entry [;
			print "ceder y ", (strong) "aceptar", " la partida";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_Pablo_tema_06);
		];

ConversationTopic _Pablo_tema_06
 class	TextSequence
 with	description
			"---Tal y como están las cosas, no creo que sea buena idea llamar 
			la atención, Pablo. Nunca se sabe quién puede estar mirando.",
		keys	!! TODO
			'insistir' 'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "insistir en que ", (strong) "no era buena idea";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_Pablo_tema_05);
			!! TODO - Cambiar disposición de Pablo *
		],
		subtopics;

!! TODO - _Pablo_tema_07 sólo tiene que estar disponible si el jugador ha 
!! llegado a ver a Mr. Panamá.

Conversation conversacion_Pablo_02
 with	topics _Pablo_tema_10 _Pablo_tema_09 _Pablo_tema_08 _Pablo_tema_07;

ConversationTopic _Pablo_tema_07
 class	TextSequence
 with	description
			"---¿Conoces a ese hombre?",
		keys	!! TODO
			'preguntar' 'hombre' 'desconocido' 'barra' 'sombrero' 'panama',
		entry [;
			print "preguntar por ", (strong) "el hombre", "de la barra";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_08
 class	TextSequence
 with	description
			"Le tendió los dardos de nuevo a Pablo. ---¿Llegará ayuda?"
			"¡Ja!... ¿De quién, de los gringos? Esos precisamente no están de 
			nuestra parte, chico. ---Mientras hablaba lanzaba con furia contra 
			la diana. ---Aquí los únicos que te van a echar una mano son los 
			muñequitos de verde, y directa al cuello. Me temo que te tienes 
			que salvar tú sólo.",
		entry [;
			print "preguntar si pueden esperar ayuda";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'esperar' 'ayuda',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_09
 class	TextSequence
 with	description
			"---¿Y qué hay de los demás? ¿El resto de la gente de Unidad?",
		entry [;
			print "preguntar por el resto de la gente del partido";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'resto' 'gente' 'partido' 'unidad',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_10
 class	TextSequence
 with	description
			"---¿Nosotros no podríamos reorganizar al pueblo y...?"
			!! TODO - calcular puntos de Pablo
			"---Baja, baja de la nube, chico. El miedo les tiene a todos bien 
			agarrados por las pelotas, ni uno dirá nada. Y aún así, ¿qué 
			querías que hicieran?, atacarlos con tu mierda de claveles y... 
			perdona, no digo más que puras tonterías. Pero es cierto, más te 
			vale que huyas, hijo. ---Le pasó los tres dardos a Gabriel después 
			de anotar 40 puntos más.",
		entry [;
			print "preguntar por lo que podían hacer entonces";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'pueden' 'podian' 'hacer' 'entonces',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_11
 class	TextSequence
 with	description
		[;
			print "Gabriel lanzó el primero de sus dardos: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"---¿Y a dónde quieres que vaya?"
			"---No importa. A cualquier lugar, donde quieras, pero lejos de 
			aquí. Y cuanto antes."
		[;
			print "Lanzó el segundo dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo seguía hablando: ---Hay una barcaza en el puerto de San 
			Antonio que sale dentro de cinco días. Ya estás corriendo si no 
			quieres que te descubran y te den morada en la cara."
		[;
			print "Su último dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			"Después de dos rondas, Pablo había conseguido XX y XX: XX puntos, 
			mientras que Gabriel XX y XX: XX puntos.";
		],
		entry [;
			print "preguntar a dónde iba a huir";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'donde' 'ir' 'huir' 'escapar' 'lugar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_12
 class	TextSequence
 with	description
			"---No soy un cobarde, Pablo. No pienso huir."
			"---¡La cobardía no tiene nada que ver con esto, chico! ¡Estamos 
			hablando de tu vida! O pones pies en polvorosa o estás perdido.",
		entry [;
			print "negarte a escapar como un cobarde";
		],
		keys	!! TODO
			'negarte' 'negar' 'niegate' 'niega' 'escapar' 'huir' 'cobarde',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_13
 class	TextSequence
 with	description
			"---De ninguna manera, Pablo. Me da igual lo que digas. No me 
			pienso mover de aquí.",
		keys	!! TODO
			'insistir' 'insiste' 'ir' 'ningun' 'sitio' 'lugar' 'no' 'huir' 
			'escapar',
		entry [;
			print "insistir en que no pensaba ir a ningún sitio";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo
		];

ConversationTopic _Pablo_tema_14
 class	TextSequence
 with	description
			"---¿De verdad no conoces a ese hombre del sombrero?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'hombre' 'desconocido' 
			'sombrero' 'panama',
		entry [;
			print "preguntar por el hombre";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_15
 class	TextSequence
 with	description
			"---¿Vendréis conmigo, Pablo?"
			"Entonces él clavó la vista en la diana, pero en realidad miraba a 
			algún lugar oculto a los ojos de Gabriel. Oculto a otros ojos 
			cualesquiera salvo a los suyos. Lanzó sus tres dardos 
			mecánicamente, uno tras otro, por 15, 20 y 18 puntos, mientras 
			sobre sus cabezas las aspas del ventilador continuaban girando con 
			su zumbido monótono. Los ecos de las demás conversaciones flotaban 
			entre ellos, extrañamente lejanas. Insignificantes. Sacudió la 
			cabeza lentamente."
			"---No.",
		entry [;
			print "preguntar si le iba a acompañar";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'acompanar' 
			'acompanara',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_16
 class	TextSequence
 with	description
			"---¿Qué? Pero... ¿pero no acabáis de decir? ¿Estáis loco o es que 
			queréis quedaros a ver cómo os dan el paseíllo?"
			"---Yo ya no tengo nada que perder, chico. Mi futuro era ver éste 
			país levantado por los chilenos unidos, pero se nos adelantaron los 
			maricas del rifle y las botas, y todo se ha jodido. En realidad es 
			como si estuviese muerto. Toda la vida luchando por un fin, y ahora 
			ese fin está más lejos que nunca. ---Contó los XX puntos que había 
			conseguido antes de devolverle los dardos a Gabriel. ---Venga, 
			lanza.",
		entry [;
			print "preguntarle si es que quería que lo ", (strong) "matasen";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'queria' 'quiere' 
			'maten' 'matasen' 'morir' 'asesinen' 'asesinarle',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_17
 class	TextSequence
 with	description
			"---¿Por qué me abandonáis?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'quiere' 'queria' 
			'abandonar' 'dejarle' 'abandonarle',
		entry [;
			print "preguntarle por qué le ", (strong) "quería abandonar";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_18
 class	TextSequence
 with	description
			"---Eres un cobarde, Pablo.",
		keys	!! TODO
			'llamarle' 'llamale' 'llama' 'cobarde' 'pablo',
		entry [;
			print "llamarle ", (strong) "cobarde";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

ConversationTopic _Pablo_tema_19
 class	TextSequence
 with	description
			"Gabriel guardó silencio.",
		keys	!! TODO
			'guarda' 'guardar' 'quedate' 'quedar' 'silencio',
		entry [;
			print "guardar silencio";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_20
 class	TextSequence
 with	description
			"Aún queda la esperanza de volver, Pablo..."
			"---¡Menuda elección me das, carajo! Esperanza o muerte. Ninguna de 
			las dos me dará lo que buscaba. Verás, en la vida creemos que 
			siempre podemos elegir, y que podemos ganar. Pero cuando te obligan 
			a escoger, entonces chico, entonces sólo nos queda perder. Y yo 
			prefiero perderlo ya todo, que no perder y llorar lo que me queda 
			de mísera vida. ---Hizo una leve pausa antes de añadir como con un 
			gruñido: ---Venga, chico. Lanza. Terminemos la partida."
			"Pablo había conseguido XX, XX y XX: XXX puntos, mientras que 
			Gabriel XX y XX: XX puntos. En silencio, se preparó para lanzar sus 
			últimos tres dardos:"
		[;
			print "Primero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Segundo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Tercero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo se encargó de hacer el recuento: ---Hacen en total XXX 
			puntos. Contra mis XXX, mira qué bien. Yo gano. ---La sonrisa que 
			se dibujó en su rostro no era alegre. No lo era en absoluto. 
			---Ten."
			"---¿Qué cosa me dáis?"
			"---Un premio de consolación. Es el billete para la barcaza de la 
			que te he hablado... ---miró a uno y otro lado antes de volver a 
			meterse la mano en el bolsillo. Un momento después le tendió el 
			revólver disimuladamente, junto con el billete. ---... y un arma, 
			por si las moscas. Cinco días son mucho tiempo, y Santiago se ha 
			convertido en una ciudad peligrosa."
			"---N-no. Yo... no puedo..."
			"---¡Pues claro que puedes, diantre! Trágate esos escrúpulos tuyos 
			por una vez, chico. Espero que no tengas que utilizarlo, sobre todo 
			después de ver la horrible puntería que tienes, pero en este 
			maldito país ya no se puede estar seguro de nada... además, aunque 
			no lo uses, yo me sentiré mejor si lo llevas contigo. Tómalo. 
			Tómalo, Gabriel. Por favor.",
		entry [;
			print "sugerir que quedaba la ", (strong) "esperanza de volver";
		],
		keys	!! TODO
			'sugerir' 'sugiere' 'sugierele' 'esperanza' 'volver' 'regreso' 
			'regresar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_21
 class	TextSequence
 with	description
			"---De verdad crees que ésta es la única opción, ¿no es cierto?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'habia' 'hay' 
			'mas' 'otras' 'opciones' 'alternativas',
		entry [;
			print "preguntar si no había ", (strong) "más opciones";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _Pablo_tema_22
 class	TextSequence
 with	description
			"---Si tú no piensas huir, yo tampoco. No voy a dejarte solo, 
			Pablo.",
		keys	!! TODO
			'di' 'dile' 'decir' 'decirle' 'piensa' 'pensaba' 'ir' 'irse' 'huir' 
			'escapar' 'sin' 'pablo',
		entry [;
			print "decirle que no pensaba irse sin él";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

!!------------------------------------------------------------------------------
!! Mr. Panamá:

!!	 *	FIXME - Nombres y adjetivos
NPC		OBJ_MrPanama "hombre del sombrero" OBJ_LIMBO
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama',
		description [;
			if (~~self.telephone_flag) return O__M(self, 1);
			else return O__M(self, 2);
		],
		react_before [;
			Look:
				!! Si el jugador intenta mirar la localidad cuando está activo 
				!! alguno de los indicadores de movimiento de Mr. Panamá, se 
				!! lanza la acción examinar a Mr. Panamá en su lugar para que 
				!! se imprima el texto del movimiento.
				if (noun == nothing)
					if (self.movement_1_flag || self.movement_2_flag 
						|| self.movement_3_flag)
						<<Examine self>>;
			Examine, Search:
				!! Si el jugador intenta mirar al teléfono mientras Mr. Panamá 
				!! está hablando por él, se imprime la descripción del hombre.
				if (noun == OBJ_telefono && self.telephone_flag)
					<<Examine self>>;
				!! Si el jugador intenta examinar a Mr. Panamá o a la clientela 
				!! cuando está activo alguno de los indicadores de movimiento, 
				!! se imprime el texto del movimiento en lugar de la 
				!! descripción del hombre o de los clientes.
				if (noun == self or OBJ_clientes) {
					if (self.movement_1_flag) {
						self.movement_1_flag = false;
						return O__M(self, 3);
					}
					if (self.movement_2_flag) {
						self.movement_2_flag = false;
						return O__M(self, 4);
					}
					if (self.movement_3_flag) {
						self.movement_3_flag = false;
						return O__M(self, 5);
					}
				}
			!! TODO - Comportamiento de TalkTo
		],
		before [;
			Attack, Pull, Push, PushDir:
				!! TODO - Cambiar mensaje
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.telephone_flag) return O__M(self, 6);
				if (self.listen_flag) return O__M(self, 8);
				self.listen_flag = true;
				return O__M(self, 7);
			TalkTo:
				return O__M(self, 9);
		],
		!! TODO - Gestión de los movimientos del personaje, usando daemon o 
		!! temporizadores.
 private
		listen_flag		false,
		movement_1_flag	false,
		movement_2_flag	false,
		movement_3_flag	false,
		telephone_flag	false,
 has	concealed male;

!!------------------------------------------------------------------------------

!! FIXME
[ LanzaDardo result;
	result = AimAndFire();
	if (result == -2)	return 10;
	if (result == 0)	return 50;
	if (result <= 1)	return 25;
	if (result <= 3)	return 10;
	if (result <= 4)	return 5;
	if (result <= 5)	return 4;
	if (result <= 6)	return 3;
	if (result <= 7)	return 2;
	if (result <= 8)	return 1;
	return 0;
];


