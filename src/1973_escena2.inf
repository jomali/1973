

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 2: Café Blanca Florida
!!
!!==============================================================================
!!
!!	File:			1973_escena2.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/02/11
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!	Este archivo forma parte de PROYECTO 1973.
!!
!!	PROYECTO 1973 es software libre: usted puede redistribuirlo y/o 
!!	modificarlo bajo los términos de la Licencia Pública General GNU 
!!	publicada por la Fundación para el Software Libre, ya sea la versión 
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	PROYECTO 1973 se distribuye con la esperanza de que sea útil, pero 
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o 
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de 
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU 
!!	junto a PROYECTO 1973. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------
!!
!!	# NOTAS
!!
!!	General:
!!	 *	FIXME - Llevar a cabo todas las inicializaciones de la escena. Además 
!!		de modificar la flexión gramatical de los mensajes de la librería, el 
!!		inventario del protagonista en esta escena es diferente del que tenía 
!!		en la escena anterior.
!!	 *	FIXME - Revisar la lógica del sistema de ayuda cuando el jugador no 
!!		empieza el relato desde la escena 1.
!!	 *	TODO - Cuando el protagonista coja el billete y el revólver que le da 
!!		Pablo, se pone fin a la ESCENA 2 y se pasa a la siguiente. Como con 
!!		toda acción clave para el desarrollo de la historia, antes de coger los 
!!		objetos se deben mostrar signos de duda en el protagonista.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	CAFE: Café Blanca Florida
!!------------------------------------------------------------------------------

Room	obj_CAFE "Café Blanca Florida"
 with	name	'cafe' 'blanca' 'florida' 'cafeteria' 'bar' 'local' 'taberna' 
				'meson' 'tasca' 'establecimiento',
		initial [; if (self hasnt visited) {
			!! Inicializaciones de la escena -----------------------------------
			!! TODO - Resto de inicializaciones
			SetGrammaticalInflection(THIRD_PERSON_PAST);
			!! Inicializaciones multimedia -------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(BAR_ENVIRONMENT, 0, 50, -1);
			Damusix.Tocar(BAR_ENVIRONMENT);
			#Endif; ! TARGET_GLULX;
			!! Introducción ----------------------------------------------------
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 2, (self has visited));
		],
		w_to	[; return self.out_to(); ],
		out_to	"No podía irse aún. No sin hablar antes con Pablo.",
		cant_go "Por ahí no podía ir a ningún lado.";

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_silla_cafe "silla" obj_CAFE
 class	SmartEnterable
 with	name_m	'asiento' 'taburete', 
		name_f	'banqueta' 'silla', 
		gender	G_FEMENINO, 
		!! FIXME - adjectives
		clarification "sentado a una de las mesas", 
		reach_objects obj_mesa_cafe obj_pablo obj_camarera,
		drop_on_object obj_mesa_cafe,
		description "Era una silla normal y corriente.", 
		react_before [;
			if (self.SmartEnterable::react_before()) return true;
			TalkTo:
				if ((parent(player) ~= self) && (noun == obj_pablo) 
				&& (obj_pablo.get_state() < 2)) {
					move player to self;
					print "Atravesó el local hasta la mesa en que se encontraba 
					Pablo.^";
					new_line;
					return false;
				}
				if ((parent(player) == self) && (noun == obj_camarera)) {
					move player to location;
				}
			Examine, Look:
				if (parent(player) == self && noun == obj_telefono)
					"Un teléfono público negro. Desde la silla no podía ver 
					mucho más.";
		],
		before [;
			Pull, Push, PushDir, Take, Turn:
				"Ya estaba", (n) self, " muy bien donde estaba", (n) self, 
				". No había ningún motivo para andar moviéndol", (o) self, ".";
			Sit:
				<<Enter self>>;
			StandUp:
				<<Exit self>>;
		],
		after [;
			Enter:
				"Se sentó en ", (the) self, ", frente a Pablo.";
			Exit:
				"Se levantó de ", (the) self, ".";
		],
 has	female supporter transparent;

!!------------------------------------------------------------------------------
!! Atrezzo:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	"mesas y sillas del café" obj_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble' 'taburete',
		name_mp	'asientos' 'muebles' 'taburetes',
		name_fp	'banquetas' 'mesas' 'sillas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 
			'corriente' 'corrientes' 'funcional' 'funcionales' 'normal' 
			'normales' 'ordinaria' 'ordinarias' 'ordinario' 'ordinarios' 
			'sencilla' 'sencillas' 'sencillo' 'sencillos' 'simple' 'simples',
		description
			"Había unas cuantas mesas y sillas de aspecto corriente 
			repartidas por todo el bar.",
		before [;
			Examine, Search, LookUnder:
				return false;
			Enter, Sit:
				<<Enter obj_silla_cafe>>;
			Exit, StandUp:
				<<Exit obj_silla_cafe>>;
			default:
				"No era necesario preocuparse por eso. Después de todo, no 
				tenía", (n) self, " nada de especial.";
		],
 has	female pluralname;

!!	 *	El atributo 'general' se activa después de que Mr. Panamá se haya ido.
!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_cliente "cliente" obj_CAFE
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama',
		description [;
			if (self hasnt general) O__M(self, 1);
			else O__M(self, 2);
			if (obj_clientes hasnt general) {
				print " ";
				<<Listen obj_clientes>>;
			}
			new_line;
			return true;
		],
		react_before [;
			if (noun ~= self) return false;
			Attack: <<Attack obj_clientes second>>;
			Listen: <<Listen obj_clientes second>>;
			Pull, Push: <<Pull obj_clientes second>>;
			TalkTo: <<TalkTo obj_clientes second>>;
		],
 has	male; !! FIXME - animate?

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_clientes "clientes" obj_CAFE
 with	name_f	'clientela' 'gente' 'persona',
		name_mp	'chicos' 'clientes' 'hombres' 'mozos' 'parroquianos' 'senores',
		name_fp	'chicas' 'mozas' 'mujeres' 'parroquinas' 'personas' 'senoras',
		gender	G_MASCULINO + G_PLURAL,
		!! FIXME - adjectives
		description [;
			SetPronoun('-les', self);
			O__M(self, 1);
			if (~~self.listen_flag) {
				print " ";
				<<Listen self>>;
			}
			new_line;
			return true;
		],
		react_before [;
			Listen:
				if (noun == nothing)
					<<Listen self>>;
		],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.listen_flag) self.listen_flag = true;
				return O__M(self, 2);
			TalkTo:
				return O__M(self, 3);
		],
 private
		!! Se activa al escuchar el objeto por primera vez.
		listen_flag false,
 has	animate male pluralname;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_humo "humo" obj_CAFE
 with	name_m	'cigarrillo' 'cigarro' 'humo' 'pitillo' 'puro' 'tabaco',
		name_f	'fumarada' 'humarada',
		name_mp	'cigarrillos' 'cigarros' 'pitillos' 'puros',
		gender	G_MASCULINO, 
		adjectives
			'apestado' 'corrompido' 'hediondo' 'infecto' 'maloliente' 
			'pestilente' 'volutas',
		description [;
			return O__M(self, 1);
		], 
		react_before [;
			Smell: 
				if (noun == nothing)
					<<Smell self>>;
		], 
		before [;
			Examine:
				return false;
			Smell:
				return O__M(self, 2);
			default:
				"No hacía falta que se preocupase por eso.";
		], 
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_mesa_cafe "mesa" obj_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble',
		name_f	'mesa',
		name_mp	'muebles',
		gender	G_FEMENINO,
		adjectives
			'corriente' 'funcional' 'normal' 'ordinaria' 'ordinario' 'pablo' 
			'sencilla' 'sencillo' 'simple',
		description [;
			print "La mesa de Pablo era una de las que estaban más al fondo en 
			el local";
			if (child(self)) {
				print " Encima había ";
				WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT);
			}
			".";
		],
		before [;
			Climb, Enter, Sit:
				"No había ningún motivo para subirse ahí.";
			Pull, Push, PushDir, Take, Turn:
				print_ret (The) self, " ya estaba", (n) self, " bien donde 
				estaba", (n) self, ". No había motivos para andar moviéndol", 
				(o) self, ".";
		],
 has	concealed female supporter transparent;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_telefono "teléfono público" obj_CAFE
 with	name_m	'auricular' 'cajetin' 'disco' 'microfono' 'microtelefono' 
				'teclado' 'telef' 'telefono' 'telf' 'tlf' 'visor',
		name_f	'cabina' 'inscripcion' 'placa' 'ruleta',
		gender	G_MASCULINO,
		adjectives
			'aparato' 'ficha' 'fichas' 'negra' 'negro' 'numerico' 'publica' 
			'publico' 'telefonica' 'telefonico',
		description [;
			return O__M(self, 1, self.tokens);
		],
		before [;
			Use, TalkOn:
				!! TODO - Contemplar opción de llamar por telf.
				"No tenía a nadie a quien llamar por teléfono en aquel 
				momento.";
		],
		after [;
			!! Se añade el tema del teléfono a la conversación con la camarera:
			obj_conversacion_camarera.add_topic(obj_camarera_tema_telefono);
		],
 private
		tokens 0,
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_ventilador "ventilador" obj_CAFE
 with	name_m	'ventilador', 
		name_f	'aspa', 
		name_fp	'aspas', 
		gender	G_FEMENINO, 
		adjectives 'encendido' 'movimiento' 'negra' 'negras' 'negro' 'techo', 
		description [; return O__M(self, 1); ],
		before [;
			Examine:
				return false;
			Listen:
				return O__M(self, 2);
			default:
				return O__M(self, 3);
		],
 has	male;

!!------------------------------------------------------------------------------
!! Furniture:

!!	 *	FIXME - Nombres y adjetivos
Atrezzo	obj_barra "barra" obj_CAFE
 with	name_m	'mobiliario' 'moblaje' 'mostrador' 'mueble',
		name_f	'barra' 'repisa',
		name_mp 'muebles',
		gender	G_FEMENINO, 
		!! FIXME - adjectives
		describe [ i obj;
			if (child(self) == nothing) return true;
			objectloop(obj in self)
				if (obj hasnt concealed or scenery or static)
					i++;
			if (i == 0) return true;
			if ((i == 1) && (obj_cerveza_gabriel in self))
				"^La cerveza de Gabriel estaba sobre la barra.";
			print "^Sobre la barra podía ver ";
			WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT + PARTINV_BIT);
			".";
		],
		description [ i obj;
			!! Se añade el tema del teléfono a la conversación con la camarera:
			obj_conversacion_camarera.add_topic(obj_camarera_tema_telefono);
			!! Descripción de la barra:
			O__M(self, 1);
			if (child(self) == nothing) return true;
			objectloop(obj in self)
				if (obj hasnt concealed or scenery or static)
					i++;
			if (i == 0) return true;
			if ((i == 1) && (obj_cerveza_gabriel in self))
				"^La cerveza de Gabriel estaba sobre la barra.";
			print "^Sobre la barra podía ver ";
			WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT + PARTINV_BIT);
			".";
		], 
		before [;
			Pull, Push, PushDir, Turn:
				"Le hubiese sido imposible mover eso.";
			Take:
				"Le hubiese sido imposible coger eso.";
		],
		after [ i obj;
			LetGo, Receive:
				objectloop(obj in self)
					if (obj hasnt concealed or scenery or static)
						i++;
				if (i == 0) give self concealed;
				else give self ~concealed;
		], 
 has	concealed female supporter;

!!	 *	FIXME - Nombres y adjetivos
Furniture	obj_diana "diana de dardos" obj_CAFE
 with	name_f	'diana',
		name_mp	'dardos',
		gender	G_FEMENINO,
		description [;
			return O__M(self, 1);
		],
 has	concealed female;

!!------------------------------------------------------------------------------
!! Items:

!!	 *	FIXME - Nombres y adjetivos
Item	obj_cerveza_gabriel "cerveza de Gabriel" obj_LIMBO
 with	name_m	'botellin', 
		name_f	'bebida' 'botella' 'cerveza', 
		gender	G_FEMENINO,
		adjectives 'alcohol' 'alcoholica' 'gabriel',
		description [;
			print "Un botellín de cerveza";
			if (self.counter == 0) " vacío.";
			if (self.counter == 1) " al que apenas le quedaba un trago.";
			if (self.counter <  4) " medio vacío.";
			if (self.counter < 10) " que Gabriel tenía a medias.";
			" sin empezar.";
		],
		before [;
			Drink:
				if (self.counter == 0) "Ya estaba vacío.";
				self.counter--;
				if (self.counter == 0) "Apuró el último trago del botellín.";
				"Gabriel dio un trago de su botellín de cerveza.";
		],
 private
		counter 0, ! Indica la cantidad de bebida que queda
 has	female;

!!	 *	Su atributo *general* se activa cuando Pablo se acaba la bebida.
!!	 *	FIXME - Nombres y adjetivos
Item	obj_vaso_vacio "bebida de Pablo" obj_mesa_cafe
 with	name_m	'tinto' 'vaso' 'vino',
		name_f	'bebida',
		gender	G_MASCULINO,
		adjectives 'alcohol' 'alcoholica' 'pablo' 'vacio',
		description [;
			if (self has general)
				"Pablo jugueteaba con el vaso vacío mientras esperaba a que 
				Gabriel fuese a por otra bebida.";
			"Un vaso de vino tinto casi vacío.";
		],
		before [;
			Examine:
				return false;
			default:
				"No tenía nada especial por lo que mereciese la pena perder el 
				tiempo con ello.";
		],
 has	female;

!!	 *	FIXME - Nombres y adjetivos
Item	obj_vino_pablo "vino de Pablo" obj_LIMBO
 with	name_m	'tinto' 'vaso' 'vino',
		name_f	'bebida',
		gender	G_MASCULINO,
		adjectives 'alcohol' 'alcoholica' 'pablo',
		description "A Pablo siempre le había gustado el tinto.",
		before [;
			Examine:
				return false;
			Drink, Pull, Push, PushDir, Take, Touch, Turn:
				"Esa era la bebida de Pablo.";
			default:
				"No tenía nada especial por lo que mereciese perder el tiempo 
				con eso.";
		],
 has	male;

!!	 *	FIXME - Nombres y adjetivos
Item	obj_billete_cafe "billete" obj_LIMBO
 with	name_m	'billete' 'papel' 'pase', 
		gender	G_MASCULINO, 
		adjectives 'antonio' 'barcaza' 'pedazo' 'san' 'trozo',
 has	male;

!!------------------------------------------------------------------------------
!! Camarera:

!!	 *	FIXME - Nombres y adjetivos
NPC		obj_camarera "camarera" obj_CAFE
 with	name_m	'camarero' 'despensero',
		name_f	'camarera' 'chica' 'dama' 'despensera' 'joven' 'moza' 
				'muchacha' 'mujer' 'persona' 'senora' 'senorita',
		gender	G_FEMENINO,
		!! FIXME - adjectives
		description [; return O__M(self, 1); ],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para comenzar una pelea.";
			TalkTo:
				ConversationManager.start(obj_conversacion_camarera);
				return true;
		],
		conversation_flag false,
 has	female concealed;

Conversation obj_conversacion_camarera
 with	talker [; print "Hablas con ", (the) obj_camarera; ],
		initial_action [;
			self.add_topic(obj_camarera_tema_cerveza);
			if (child(self)) {
				self.add_temporal_topic(obj_camarera_tema_temporal);
			}
			print "Gabriel se acercó hasta la barra.^";
			new_line;
			print (string) random (
				"---¿Quieres alguna cosa?",
				"---¿Te sirvo alguna cosa?",
				"---¿Qué vas a tomar?",
				"---¿Qué tomas?",
				"---¿Qué te sirvo?",
			);
			new_line;
			return true;
		],
		inter_action
			"La camarera observaba a Gabriel desde el otro lado de la barra.",
		final_action
			"No llegó a cruzar ninguna otra palabra con ella.";

ConversationTopic	obj_camarera_tema_temporal
 with	entry [; print "no pedir ", (strong) "nada"; ],
		keys
			'cosa' 'nada' 'ninguna' 'pedir' 'pedirla' 'pide' 'pidela',
		reply
			"No quería nada en realidad. Dio las gracias a la mujer y se alejó 
			de nuevo de la barra.",
		reaction [; ConversationManager.end(); ],
		persistent true;

ConversationTopic	obj_camarera_tema_cerveza
 with	entry [; print "pedir una ", (strong) "bebida"; ],
		keys
			'bebida' 'cerveza' 'pedir' 'pedirla' 'pide' 'pidela' 'trago',
		reply
			"---¿Puede ponerme una cerveza, por favor?
			^^
			---Claro, ---un momento después le puso un botellín de cerveza 
			sobre la barra. ---Ahí tiene.",
		reaction [;
			!! FIXME
			move obj_cerveza_gabriel to obj_barra;
			give obj_barra ~concealed;
		];

ConversationTopic	obj_camarera_tema_bebida
 with	entry [;
			if (obj_cerveza_gabriel in obj_LIMBO)
				print "pedir un ", (strong) "par de bebidas";
			else
				print "pedir un ", (strong) "vino";
		],
		keys
			'bebida' 'bebidas' 'cerveza' 'copa' 'copas' 'pedir' 'pedirla' 
			'pide' 'pidela' 'tinto' 'trago' 'tragos' 'vino',
		reply [;
			if (obj_cerveza_gabriel in obj_LIMBO)
				print "Pidió una cerveza y un tinto a la camarera, y volvió 
				hasta la mesa con las bebidas.";
			else
				print "Pidió un tinto a la camarera y volvió luego hasta la 
				mesa con la bebida.";
			" Mientras cogía su vaso, Pablo señaló la diana que había colgada 
			en una esquina.
			^^
			---No te sientes. Ven, juguemos.";
		],
		reaction [;
			!! Se modifica la situación con la camarera:
			if ((obj_camarera_tema_telefono has visited) 
			&& (obj_camarera_tema_desconocido has visited)) {
				give obj_conversacion_camarera general;
				ConversationManager.end();
			}
			!! Se modifica la situación con Pablo:
			give obj_pablo_tema_03 visited;
			give obj_pablo_tema_04 visited;
			give obj_pablo_tema_05 visited;
			obj_conversacion_pablo.remove_topic(obj_pablo_tema_03);
			obj_conversacion_pablo.remove_topic(obj_pablo_tema_04);
			obj_conversacion_pablo.remove_topic(obj_pablo_tema_05);
			obj_conversacion_pablo.add_topic(obj_pablo_tema_06);
			obj_conversacion_pablo.add_topic(obj_pablo_tema_07);
			obj_conversacion_pablo.set_state(3);
			new_line;
			ConversationManager.start(obj_conversacion_pablo, true);
		],
		append_topic_inventory false;

ConversationTopic	obj_camarera_tema_telefono
 with	entry [; print "preguntar por el ", (strong) "teléfono"; ],
		keys
			'pregunta' 'preguntala' 'preguntar' 'preguntarla' 'telef' 
			'telefono' 'telf' 'tlf',
		reply [;
"---¿El teléfono funciona?
^^
---Si, claro. Tienes que meterle fichas para llamar. ¿Quieres comprar alguna?
^^
Se lo pensó un segundo, pero en realidad no sabía muy bien a quién podría telefonear. Conocía a gente, por supuesto, solía salir con compañeros del partido o del Instituto Pedagógico, sin embargo, apenas tenía amigos de verdad. Pablo era de los pocos, y ya estaba con él. Dio las gracias a la camarera, y con la misma volvió a alejarse de la barra, sin comprar nada.";
		],
		reaction [;
			!! Se modifica la situación con la camarera:
			if (obj_camarera_tema_desconocido has visited)
				give obj_conversacion_camarera general;
			ConversationManager.end();
		];

!! TODO - Terminar de implementar
ConversationTopic	obj_camarera_tema_desconocido
 with	entry [;
			print "preguntarla por el ", (strong) "hombre", " del sombrero";
		],
		keys
			'hombre' 'sombrero' 'desconocido',
		reaction [;
			!! Se modifica la situación con la camarera:
			if (obj_camarera_tema_telefono has visited) {
				give obj_conversacion_camarera general;
				ConversationManager.end();
			}
		];

!!------------------------------------------------------------------------------
!! Pablo:

!!	 *	FIXME - Nombres y adjetivos
NPC		obj_pablo "Pablo" obj_CAFE
 with	name_m	'pablo' 'ureta' 'amigo' 'hombre', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! FIXME - adjectives
		describe [;
			new_line;
			if (~~self.describe_flag) {
				self.describe_flag = true;
				return O__M(self, 1);
			}
			switch (self.state) {
				0:	return O__M(self, 2, (parent(player) ~= obj_silla_cafe));
				1:	return O__M(self, 3, (parent(player) ~= obj_silla_cafe));
				2:	return O__M(self, 4);
				3:	return O__M(self, 5);
				default: return O__M(self, 6);
			}
		],
		description [;
			switch (self.state) {
				0:	if (parent(player) ~= obj_silla_cafe)
						print "Estaba sentado en una de las mesas del fondo, 
						con la vista clavada en su vaso de tinto. ";
					return O__M(self, 7);
				1:	if (parent(player) ~= obj_silla_cafe)
						print "Estaba sentado en una de las mesas del fondo, 
						con la mirada perdida en algún lugar desconocido. ";
					return O__M(self, 7);
				2:	return O__M(self, 7);
				3:	return O__M(self, 8);
				default: return O__M(self, 9);
			}
		],
		before [;
			TalkTo:
				!! TODO - Añadir a Mr. Panamá al café
				ConversationManager.start(obj_conversacion_pablo);
				return true;
		],
		!! Funciones de acceso y modificación de los atributos privados:
		get_drink_flag [;
			return self.drink_flag;
		],
		get_state [;
			return self.state;
		],
		increase_disposition [ i;
			if (i == 0) self.disposition++;
			else self.disposition = self.disposition + i;
			return true;
		],
		set_drink_flag [ i;
			self.drink_flag = i;
			return true;
		],
		set_state [ i;
			if (i < 0) return false;
			self.state = i;
			return true;
		],
 private
		!! (booleano) Indica si la propiedad *describe* del objeto se ha 
		!! ejecutado o no:
		describe_flag false,
		!! (entero) Disposición de Pablo hacia Gabriel. Cuanto mayor es el 
		!! valor, más enfadado está con él:
		disposition 0,
		!! (booleano) Indica si Gabriel ha intentado convencer a Pablo de que 
		!! no siga bebiendo:
		drink_flag false,
		!! (entero) Estado de Pablo.
		!!	 *	0: Bebe sentado en una de las mesas del café.
		!!	 *	1: Sentado en una mesa, con el vaso vacío.
		!!	 *	2: Sentado en una mesa, proponiendo una partida a Gabriel.
		!!	 *	3: Jugando a los dardos.
		!!	 *	4: Espera a que Gabriel coja el billete y el revólver.
		state 0,
 has	male proper;

Conversation obj_conversacion_pablo
 with	talker [; print "Hablas con ", (the) obj_pablo; ],
		initial_action [;
			switch (self.state) {
				0:	self.set_state(1);
					self.add_topic(obj_pablo_tema_01);
					self.add_topic(obj_pablo_tema_02);
					"---Ahí estás. Llegas tarde, chico.";
				1:	"---¿Qué, chico? Tarde, pero has venido. ¿Es que ahora no 
					vas a decir nada?";
				2:	PrintOrRun(self, inter_action);
					return true;
				3:	"---Venga, juguemos.";
				default:
					PrintOrRun(self, inter_action);
					return true;
			}
		],
		inter_action [;
			switch (self.state) {
				1:	"---¿Y bien? ¿Es que no vas a decir nada?";
				2:	"Pablo señaló el vaso vacío que tenía delante.
					^^
					---Hazme el favor y pídeme otro, ¿quieres?";
				3:	"---¿A qué esperas? Vamos.";
				default:
					"Pablo miraba paciente a Gabriel.";
			}
		],
		!! Funciones de acceso y modificación de los atributos privados:
		get_state [;
			return self.state;
		],
		set_state [ i;
			if (i < 0) return false;
			self.state = i;
			switch (self.state) {
				0,1: obj_pablo.set_state(0);
				2: obj_pablo.set_state(1);
				3: obj_pablo.set_state(2);
				!! FIXME - Resto de estados
			}
			self.state = i;
			return true;
		],
 private
		!! (entero) Estado de la conversación:
		!!	 *	0: Estado inicial, antes de empezar la conversación.
		!!	 *	1: Conversación iniciada, antes de que Gabriel conteste.
		!!	 *	2: Pablo esperando a que Gabriel le traiga otra copa.
		!!	 *	3: Pablo espera a que Gabriel acepte la partida a los dardos.
		!!	 *	FIXME - Documentar resto de estados.
		state 0;

ConversationTopic	obj_pablo_tema_01
 with	entry [; print "pedirle ", (strong) "perdón", " por el retraso"; ],
		keys
			'disculpa' 'discupas' 'excusas' 'pedir' 'pedirle' 'perdon' 'pide' 
			'pidele' 'retraso' 'tarde',
		reply
"---Siento llegar tarde. He tenido que dar un rodeo enorme para llegar hasta aquí. Hay soldados por toda la ciudad.
^^
---Lo sé, lo sé. No te disculpes. Ya no hay sitio seguro en toda la capital. Tal como están las cosas, es no más cuestión de tiempo que nos agarren esos mamones con pistola... sin Allende, todo está perdido.
^^
Terminó lo que le quedaba en el vaso y se quedó en silencio, con una mueca desagradable en la cara y la mirada perdida en algún lugar, lejos de aquel bar. Cuando volvió en si, un momento después, agitó suavemente el vaso vacío que sostenía en la mano.
^^
---Hazme un favor, chico. Pídeme otro, ¿quieres?",
		reaction [;
			!! Se modifica la situación con Pablo:
			obj_conversacion_pablo.set_state(2);
			!! Se modifica la situación con la camarera:
			give obj_camarera_tema_cerveza visited;
			obj_conversacion_camarera.remove_topic(obj_camarera_tema_cerveza);
			obj_conversacion_camarera.add_topic(obj_camarera_tema_bebida);
		],
		relatives obj_pablo_tema_02,
		subtopics obj_pablo_tema_03 obj_pablo_tema_04 obj_pablo_tema_05,
		append_topic_inventory false;

ConversationTopic	obj_pablo_tema_02
 with	entry [;
			print "llamarle ", (strong) "loco", " por citarle allí en un 
			momento como aquel";
		],
		keys
			'aquel' 'citarle' 'llama' 'llamale' 'llamar' 'llamarle' 'loco' 
			'momento',
		reply
"---¿Es que estás loco, Pablo? ¿Cómo se te ha ocurrido citarme aquí justo ahora? Hay soldados por todas partes, maldita sea. He tenido que dar un rodeo enorme para llegar hasta aquí sin cruzarme con ninguno.
^^
---¡Noticias frescas! Claro que hay soldados. La ciudad entera se ha convertido en un hormiguero, carajo. Tal como están las cosas, es no más cuestión de tiempo que nos agarren esos mamones con pistola... sin Allende, todo está perdido.
^^
Terminó lo que le quedaba en el vaso y se quedó en silencio, con una mueca desagradable en la cara y la mirada perdida en algún lugar, lejos de aquel bar. Cuando volvió en si, un momento después, agitó suavemente el vaso vacío que sostenía en la mano.
^^
---Haz algo, chico. Pídeme otro, ¿quieres?",
		reaction [;
			!! Se modifica la situación con Pablo:
			obj_pablo.increase_disposition();
			obj_conversacion_pablo.set_state(2);
			!! Se modifica la situación con la camarera:
			give obj_camarera_tema_cerveza visited;
			obj_conversacion_camarera.remove_topic(obj_camarera_tema_cerveza);
			obj_conversacion_camarera.add_topic(obj_camarera_tema_bebida);
		],
		relatives obj_pablo_tema_01,
		subtopics obj_pablo_tema_03 obj_pablo_tema_04 obj_pablo_tema_05,
		append_topic_inventory false;

ConversationTopic	obj_pablo_tema_03
 with	entry [; print "preguntarle por ", (strong) "Allende"; ],
		keys
			'allende' 'pregunta' 'preguntale' 'preguntar' 'preguntarle',
		reply
"---Es verdad, ¿no es cierto? Eso que dicen. Que Allende...
^^
---Para, para un momento. No me hagas hablar con la garganta seca, anda... hoy menos que ningún otro día.",
		append_topic_inventory false;

ConversationTopic	obj_pablo_tema_04
 with	entry [; print "ir a pedirle otro ", (strong) "vino"; ],
		keys
			'bebida' 'copa' 'otro' 'pedir' 'pedirle' 'pide' 'pidele' 'tinto' 
			'vino',
		reply
"---Vale. Ahora te lo traigo, espera un momento.",
		reaction [;
			move player to location;
		],
		relatives obj_pablo_tema_03 obj_pablo_tema_05,
		append_topic_inventory false;

ConversationTopic	obj_pablo_tema_05
 with	entry [; print "decirle que no debería seguir ", (strong) "bebiendo"; ],
		keys
			'bebiendo' 'deberia' 'decir' 'decirle' 'di' 'dile' 'no' 'seguir',
		reply
"---Tal vez no deberías beber más, ¿no crees?
^^
---¿Qué? ¿Qué se supone que quieres decir con eso, chico? Deja de hacer el gilipollas y haz el favor de traerme otra condenada copa.",
		reaction [;
			!! Se modifica la situación con Pablo:
			obj_pablo.increase_disposition();
			obj_pablo.set_drink_flag(true);
		],
		append_topic_inventory false;

!!	 *	El atributo *general* se activa al tratar el tema *obj_pablo_tema_07*.
ConversationTopic	obj_pablo_tema_06
 with	entry [;
			if (self has general) print "ceder y ";
			print (strong) "aceptar", " la partida";
		],
		keys
			'acepta' 'aceptar' 'cede' 'ceder' 'dardos' 'juego' 'partida',
		reply
"Gabriel hubiese preferido permanecer sentado en aquella mesa, al fondo del café, donde más desapercibidos podían pasar. Pero decidió seguir a Pablo hasta la diana, esperando que nadie se fijase mucho en ellos.
^^
---Has oído lo que ha ocurrido, ¿verdad? ---hablaba mientras cogía los dardos y se colocaba frente a la diana.",
		relatives obj_pablo_tema_07 obj_pablo_tema_08;

ConversationTopic	obj_pablo_tema_07
 with	entry [;
			print "decirle que no les convenía ", (strong) "llamar la atención";
		],
		keys
			'atencion' 'convenia' 'decir' 'decirle' 'di' 'dile' 'llamar',
		reply [;
print "---No creo que sea buena idea. Deberíamos procurar llamar la atención lo menos posible, Pablo. Nunca se sabe quién podría estar mirando.
^^";
		if (obj_pablo.get_drink_flag())
"---¿Y qué llama la atención de dos amigos jugando a los dardos en un bar? ¿O es que te preocupa que esté demasiado borracho como para poder tirar unos dardos? ¿Eh? Siempre pecas de ser demasiado prudente, chico. Venga.";
		else
"---¿Y qué llama la atención de dos amigos jugando a los dardos en un bar? Siempre pecas de ser demasiado prudente, chico. Venga, juguemos. ¿Quién iba a fijarse en nosotros por eso?";
		],
		reaction [; give obj_pablo_tema_06 general; ],
		subtopics obj_pablo_tema_08;

ConversationTopic	obj_pablo_tema_08
 with	entry [;
			print "insistir en que no les convenía ", (strong) "llamar la 
			atención";
		],
		keys
			'atencion' 'convenia' 'insiste' 'insistele' 'insistir' 'insistirle' 
			'llamar',
		reply 
"---De verdad, Pablo. Es peligroso.
^^
Le espetó que dejase de ser un mojigato y acto seguido se levantó bruscamente de la mesa, ante la mirada curiosa de un par de personas sentadas cerca, y se dirigió hacia la diana. Gabriel lo siguió. ¿Qué otra cosa podía hacer?
^^
---Has oído lo que ha ocurrido, ¿verdad? ---hablaba mientras cogía los dardos y se colocaba frente a la diana.",
		reaction [;
			obj_pablo.increase_disposition();
		],
		relatives obj_pablo_tema_06;






ConversationTopic _pablo_tema_03
 class	TextSequence
 with	description
			"Pablo se hizo hasta la barra para devolver el vaso vacío y pedir 
			un juego de dardos a la camarera.
			^^
			---Has oido lo que ha ocurrido, ¿verdad? ---Hablaba mientras se 
			colocaba frente a la diana y empezaba a lanzar dardos."
			"---Dicen que lo mataron, pero era demasiado orgulloso para dejarse 
			arrastrar. Los de arriba hablan de que se dio el viaje..."
			"---¡Qué sabrán los de arriba! ---Su dardo se clavó con bastante 
			más fuerza de la necesaria. ---Ellos jamás creyeron en Salvador. Ya 
			antes le dieron la patada, ahora no iban a hacer menos. No, él no 
			huiría, lo dejó bien claro. Estoy seguro de que al final ha pagado 
			la lealtad del pueblo, pero también la deslealtad de los cabrones 
			de Unidad, que le dejaron bien en la Moneda con el culo al aire. 
			---Hizo entonces una pausa y lanzó su último dardo. ---No sé ya si 
			lo mataron o se mató él, pero estoy seguro de que..."
			"---No lo digáis..."
			!! TODO - calcular puntos de Pablo
			"---... está ya criando malvas, chico, y eso no lo vamos a 
			solucionar ni tú ni to. Estamos bien jodidos. ---Arrancó los dardos 
			de la diana y se los tendió luego a Gabriel. ---Quince y diez y 
			doce. Treinta y siente puntos. Ten. Tu turno.",
		entry [;
			print (strong) "aceptar", " partida";
		],
		keys	!! TODO
			'aceptar' 'partida' 'juego',
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_pablo_tema_04);
		];

ConversationTopic _pablo_tema_04
 class	TextSequence
 with	description
			"---No creo que sea buena idea llamar la atención.",
		keys	!! TODO
			'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "decirle que ", (strong) "no es buena idea", " llamar la 
			atención";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_pablo_tema_03);
		],
		subtopics _pablo_tema_05 _pablo_tema_06;

ConversationTopic _pablo_tema_05
 class	TextSequence
 with	description
			"---Vale. Echemos una partida.",
		keys	!! TODO
			'ceder' 'aceptar' 'partida' 'juego',
		entry [;
			print "ceder y ", (strong) "aceptar", " la partida";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_pablo_tema_06);
		];

ConversationTopic _pablo_tema_06
 class	TextSequence
 with	description
			"---Tal y como están las cosas, no creo que sea buena idea llamar 
			la atención, Pablo. Nunca se sabe quién puede estar mirando.",
		keys	!! TODO
			'insistir' 'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "insistir en que ", (strong) "no era buena idea";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(_pablo_tema_05);
			!! TODO - Cambiar disposición de Pablo *
		],
		subtopics;

!! TODO - _pablo_tema_07 sólo tiene que estar disponible si el jugador ha 
!! llegado a ver a Mr. Panamá.

Conversation conversacion_pablo_02
 with	topics _pablo_tema_10 _pablo_tema_09 _pablo_tema_08 _pablo_tema_07;

ConversationTopic _pablo_tema_07
 class	TextSequence
 with	description
			"---¿Conoces a ese hombre?",
		keys	!! TODO
			'preguntar' 'hombre' 'desconocido' 'barra' 'sombrero' 'panama',
		entry [;
			print "preguntar por ", (strong) "el hombre", "de la barra";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_08
 class	TextSequence
 with	description
			"Le tendió los dardos de nuevo a Pablo. ---¿Llegará ayuda?"
			"¡Ja!... ¿De quién, de los gringos? Esos precisamente no están de 
			nuestra parte, chico. ---Mientras hablaba lanzaba con furia contra 
			la diana. ---Aquí los únicos que te van a echar una mano son los 
			muñequitos de verde, y directa al cuello. Me temo que te tienes 
			que salvar tú sólo.",
		entry [;
			print "preguntar si pueden esperar ayuda";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'esperar' 'ayuda',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_09
 class	TextSequence
 with	description
			"---¿Y qué hay de los demás? ¿El resto de la gente de Unidad?",
		entry [;
			print "preguntar por el resto de la gente del partido";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'resto' 'gente' 'partido' 'unidad',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_10
 class	TextSequence
 with	description
			"---¿Nosotros no podríamos reorganizar al pueblo y...?"
			!! TODO - calcular puntos de Pablo
			"---Baja, baja de la nube, chico. El miedo les tiene a todos bien 
			agarrados por las pelotas, ni uno dirá nada. Y aún así, ¿qué 
			querías que hicieran?, atacarlos con tu mierda de claveles y... 
			perdona, no digo más que puras tonterías. Pero es cierto, más te 
			vale que huyas, hijo. ---Le pasó los tres dardos a Gabriel después 
			de anotar 40 puntos más.",
		entry [;
			print "preguntar por lo que podían hacer entonces";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'pueden' 'podian' 'hacer' 'entonces',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_11
 class	TextSequence
 with	description
		[;
			print "Gabriel lanzó el primero de sus dardos: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"---¿Y a dónde quieres que vaya?"
			"---No importa. A cualquier lugar, donde quieras, pero lejos de 
			aquí. Y cuanto antes."
		[;
			print "Lanzó el segundo dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo seguía hablando: ---Hay una barcaza en el puerto de San 
			Antonio que sale dentro de cinco días. Ya estás corriendo si no 
			quieres que te descubran y te den morada en la cara."
		[;
			print "Su último dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			"Después de dos rondas, Pablo había conseguido XX y XX: XX puntos, 
			mientras que Gabriel XX y XX: XX puntos.";
		],
		entry [;
			print "preguntar a dónde iba a huir";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'donde' 'ir' 'huir' 'escapar' 'lugar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_12
 class	TextSequence
 with	description
			"---No soy un cobarde, Pablo. No pienso huir."
			"---¡La cobardía no tiene nada que ver con esto, chico! ¡Estamos 
			hablando de tu vida! O pones pies en polvorosa o estás perdido.",
		entry [;
			print "negarte a escapar como un cobarde";
		],
		keys	!! TODO
			'negarte' 'negar' 'niegate' 'niega' 'escapar' 'huir' 'cobarde',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_13
 class	TextSequence
 with	description
			"---De ninguna manera, Pablo. Me da igual lo que digas. No me 
			pienso mover de aquí.",
		keys	!! TODO
			'insistir' 'insiste' 'ir' 'ningun' 'sitio' 'lugar' 'no' 'huir' 
			'escapar',
		entry [;
			print "insistir en que no pensaba ir a ningún sitio";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo
		];

ConversationTopic _pablo_tema_14
 class	TextSequence
 with	description
			"---¿De verdad no conoces a ese hombre del sombrero?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'hombre' 'desconocido' 
			'sombrero' 'panama',
		entry [;
			print "preguntar por el hombre";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_15
 class	TextSequence
 with	description
			"---¿Vendréis conmigo, Pablo?"
			"Entonces él clavó la vista en la diana, pero en realidad miraba a 
			algún lugar oculto a los ojos de Gabriel. Oculto a otros ojos 
			cualesquiera salvo a los suyos. Lanzó sus tres dardos 
			mecánicamente, uno tras otro, por 15, 20 y 18 puntos, mientras 
			sobre sus cabezas las aspas del ventilador continuaban girando con 
			su zumbido monótono. Los ecos de las demás conversaciones flotaban 
			entre ellos, extrañamente lejanas. Insignificantes. Sacudió la 
			cabeza lentamente."
			"---No.",
		entry [;
			print "preguntar si le iba a acompañar";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'acompanar' 
			'acompanara',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_16
 class	TextSequence
 with	description
			"---¿Qué? Pero... ¿pero no acabáis de decir? ¿Estáis loco o es que 
			queréis quedaros a ver cómo os dan el paseíllo?"
			"---Yo ya no tengo nada que perder, chico. Mi futuro era ver éste 
			país levantado por los chilenos unidos, pero se nos adelantaron los 
			maricas del rifle y las botas, y todo se ha jodido. En realidad es 
			como si estuviese muerto. Toda la vida luchando por un fin, y ahora 
			ese fin está más lejos que nunca. ---Contó los XX puntos que había 
			conseguido antes de devolverle los dardos a Gabriel. ---Venga, 
			lanza.",
		entry [;
			print "preguntarle si es que quería que lo ", (strong) "matasen";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'queria' 'quiere' 
			'maten' 'matasen' 'morir' 'asesinen' 'asesinarle',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_17
 class	TextSequence
 with	description
			"---¿Por qué me abandonáis?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'quiere' 'queria' 
			'abandonar' 'dejarle' 'abandonarle',
		entry [;
			print "preguntarle por qué le ", (strong) "quería abandonar";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_18
 class	TextSequence
 with	description
			"---Eres un cobarde, Pablo.",
		keys	!! TODO
			'llamarle' 'llamale' 'llama' 'cobarde' 'pablo',
		entry [;
			print "llamarle ", (strong) "cobarde";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

ConversationTopic _pablo_tema_19
 class	TextSequence
 with	description
			"Gabriel guardó silencio.",
		keys	!! TODO
			'guarda' 'guardar' 'quedate' 'quedar' 'silencio',
		entry [;
			print "guardar silencio";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_20
 class	TextSequence
 with	description
			"Aún queda la esperanza de volver, Pablo..."
			"---¡Menuda elección me das, carajo! Esperanza o muerte. Ninguna de 
			las dos me dará lo que buscaba. Verás, en la vida creemos que 
			siempre podemos elegir, y que podemos ganar. Pero cuando te obligan 
			a escoger, entonces chico, entonces sólo nos queda perder. Y yo 
			prefiero perderlo ya todo, que no perder y llorar lo que me queda 
			de mísera vida. ---Hizo una leve pausa antes de añadir como con un 
			gruñido: ---Venga, chico. Lanza. Terminemos la partida."
			"Pablo había conseguido XX, XX y XX: XXX puntos, mientras que 
			Gabriel XX y XX: XX puntos. En silencio, se preparó para lanzar sus 
			últimos tres dardos:"
		[;
			print "Primero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Segundo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Tercero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo se encargó de hacer el recuento: ---Hacen en total XXX 
			puntos. Contra mis XXX, mira qué bien. Yo gano. ---La sonrisa que 
			se dibujó en su rostro no era alegre. No lo era en absoluto. 
			---Ten."
			"---¿Qué cosa me dáis?"
			"---Un premio de consolación. Es el billete para la barcaza de la 
			que te he hablado... ---miró a uno y otro lado antes de volver a 
			meterse la mano en el bolsillo. Un momento después le tendió el 
			revólver disimuladamente, junto con el billete. ---... y un arma, 
			por si las moscas. Cinco días son mucho tiempo, y Santiago se ha 
			convertido en una ciudad peligrosa."
			"---N-no. Yo... no puedo..."
			"---¡Pues claro que puedes, diantre! Trágate esos escrúpulos tuyos 
			por una vez, chico. Espero que no tengas que utilizarlo, sobre todo 
			después de ver la horrible puntería que tienes, pero en este 
			maldito país ya no se puede estar seguro de nada... además, aunque 
			no lo uses, yo me sentiré mejor si lo llevas contigo. Tómalo. 
			Tómalo, Gabriel. Por favor.",
		entry [;
			print "sugerir que quedaba la ", (strong) "esperanza de volver";
		],
		keys	!! TODO
			'sugerir' 'sugiere' 'sugierele' 'esperanza' 'volver' 'regreso' 
			'regresar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_21
 class	TextSequence
 with	description
			"---De verdad crees que ésta es la única opción, ¿no es cierto?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'habia' 'hay' 
			'mas' 'otras' 'opciones' 'alternativas',
		entry [;
			print "preguntar si no había ", (strong) "más opciones";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic _pablo_tema_22
 class	TextSequence
 with	description
			"---Si tú no piensas huir, yo tampoco. No voy a dejarte solo, 
			Pablo.",
		keys	!! TODO
			'di' 'dile' 'decir' 'decirle' 'piensa' 'pensaba' 'ir' 'irse' 'huir' 
			'escapar' 'sin' 'pablo',
		entry [;
			print "decirle que no pensaba irse sin él";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

!!------------------------------------------------------------------------------
!! Mr. Panamá:

!!	 *	FIXME - Nombres y adjetivos
NPC		obj_MrPanama "hombre del sombrero" obj_LIMBO
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama',
		description [;
			if (~~self.telephone_flag) return O__M(self, 1);
			else return O__M(self, 2);
		],
		react_before [;
			Look:
				!! Si el jugador intenta mirar la localidad cuando está activo 
				!! alguno de los indicadores de movimiento de Mr. Panamá, se 
				!! lanza la acción examinar a Mr. Panamá en su lugar para que 
				!! se imprima el texto del movimiento.
				if (noun == nothing)
					if (self.movement_1_flag || self.movement_2_flag 
						|| self.movement_3_flag)
						<<Examine self>>;
			Examine, Search:
				!! Si el jugador intenta mirar al teléfono mientras Mr. Panamá 
				!! está hablando por él, se imprime la descripción del hombre.
				if (noun == obj_telefono && self.telephone_flag)
					<<Examine self>>;
				!! Si el jugador intenta examinar a Mr. Panamá o a la clientela 
				!! cuando está activo alguno de los indicadores de movimiento, 
				!! se imprime el texto del movimiento en lugar de la 
				!! descripción del hombre o de los clientes.
				if (noun == self or obj_clientes) {
					if (self.movement_1_flag) {
						self.movement_1_flag = false;
						return O__M(self, 3);
					}
					if (self.movement_2_flag) {
						self.movement_2_flag = false;
						return O__M(self, 4);
					}
					if (self.movement_3_flag) {
						self.movement_3_flag = false;
						return O__M(self, 5);
					}
				}
			!! TODO - Comportamiento de TalkTo
		],
		before [;
			Attack, Pull, Push, PushDir:
				!! TODO - Cambiar mensaje
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.telephone_flag) return O__M(self, 6);
				if (self.listen_flag) return O__M(self, 8);
				self.listen_flag = true;
				return O__M(self, 7);
			TalkTo:
				return O__M(self, 9);
		],
		!! TODO - Gestión de los movimientos del personaje, usando daemon o 
		!! temporizadores.
 private
		listen_flag		false,
		movement_1_flag	false,
		movement_2_flag	false,
		movement_3_flag	false,
		telephone_flag	false,
 has	concealed male;

!!------------------------------------------------------------------------------

!! FIXME
[ LanzaDardo result;
	result = AimAndFire();
	if (result == -2)	return 10;
	if (result == 0)	return 50;
	if (result <= 1)	return 25;
	if (result <= 3)	return 10;
	if (result <= 4)	return 5;
	if (result <= 5)	return 4;
	if (result <= 6)	return 3;
	if (result <= 7)	return 2;
	if (result <= 8)	return 1;
	return 0;
];


