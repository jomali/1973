

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 2: Café Blanca Florida
!!
!!==============================================================================
!!
!!	File:			1973_escena2.inf
!!	Author(s):		J. Francisco Martín (jfm.lisaso@gmail.com)
!!	Languague:		ES (Castellano)
!!	System:			Inform, INFSP 6
!!	Platform:		Z-Machine / GLULX
!!	Version:		1.0
!!	Released:		2014/02/01
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!	This file is part of PROYECTO 1973.
!!
!!	PROYECTO 1973 is free software: you can redistribute it and/or modify 
!!	it under the terms of the GNU General Public License as published by 
!!	the Free Software Foundation, either version 3 of the License, or 
!!	(at your option) any later version.
!!
!!	PROYECTO 1973 is distributed in the hope that it will be useful, 
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of 
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License 
!!	along with PROYECTO 1973. If not, see <http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	CAFE: Café Blanca Florida
!!------------------------------------------------------------------------------

Room	CAFE "Café Blanca Florida"
 with	name	'cafe' 'blanca' 'florida' 'cafeteria' 'bar' 'local' 'taberna' 
				'meson' 'tasca' 'establecimiento',
		initial [; if (self hasnt visited) {
			!! Inicializaciones de la escena -----------------------------------
			SetGrammaticalInflection(THIRD_PERSON_PAST);
			!! TODO - Contenidos de la ropa
			!! Inicializaciones multimedia -------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(BAR_ENVIRONMENT, 0, 50, -1);
			Damusix.Tocar(BAR_ENVIRONMENT);
			#Endif; ! TARGET_GLULX;
			!! Introducción ----------------------------------------------------
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 2);
		],
		w_to	[; return self.out_to(); ],
		out_to	"No podía irse aún. No sin hablar antes con Pablo.",
		cant_go "Esa dirección no llevaba a ningún lado.", 
;

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

Atrezzo	-> silla "silla" 
 class	SmartEnterable
 with	clarification "sentado a una de las mesas", 
		name_m	'asiento' 'taburete', 
		name_f	'silla' 'banqueta', 
		gender	G_FEMENINO, 
		!! TODO: adjectives
		description "Una silla normal y corriente.", 
		before [;
			Pull, Push, PushDir, Take, Turn:
				"Ya estaba", (n) self, " muy bien donde estaba", (n) self, 
				". No había ningún motivo para andar moviéndol", (o) self, ".";
			Sit:
				<<Enter self>>;
			StandUp:
				<<Exit self>>;
		],
		after [;
			Enter:	"Se sentó en ", (the) self, ", frente a Pablo.";
			Exit:	"Se levantó de ", (the) self, ".";
		],
 has	female enterable supporter transparent;

!!------------------------------------------------------------------------------
!! Atrezzo:

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Items:

!!------------------------------------------------------------------------------
!! NPCs:

NPC		Pablo "Pablo" CAFE
 with	name_m	'pablo' 'ureta' 'amigo' 'hombre', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! TODO: adjectives
		describe [;
			if (~~self.description_flag) {
				self.description_flag = true;
				new_line;
				return O__M(self, 1);
			}
			else {
				print "^Pablo estudiaba su cerveza en silencio, sentado";
				if (player in silla) " frente a Gabriel.";
				else " solo en una de las mesas más alejadas.";
			}
		],
		description [;
			if (player notin silla)
				print "Estaba sentado en una de las mesas del fondo, con la 
				vista clavada en su botellín de cerveza. ";
			return O__M(self, 2);
		],
		before [;
			TalkTo:
				print "Atravesó el local hasta la mesa en que se encontraba 
				Pablo.^";
				new_line;
				print "---Ahí estás. Llegas tarde, chico.^";
				new_line;
				ConversationManager.start(conversacion_pablo);
				return true;
		], 
 private
		description_flag false,	! indica si el objeto ha sido descrito
 has	male proper;

!!------------------------------------------------------------------------------
!! Conversations:

Conversation conversacion_pablo
 with	topics e2_02 e2_01;

ConversationEntry e2_01
 with	entry [;
			print (strong) "pedir disculpas", " por el retraso";
		], 
		keys
			'disculparse' 'pedir' 'pide' 'disculpa' 'disculpas' 'disculpate'
			'lo' 'siento', 
		reply [;
			"---Lo sé, lo sé. Lo siento. Hay soldados por todas partes. He 
			venido dando un rodeo.";
		], 
		reaction [;
			parent(self).remove_topic(e2_02);
		];

ConversationEntry e2_02
 with	entry [;
			print (strong) "llamar loco", " a Pablo por cirtarle allí en un 
			momento como aquel";
		], 
		keys
			'llamarle' 'loco' 'citarle' 'alli' 'momento' 'aquel', 
		reply [;
			"---¿Es que estás loco, Pablo? ¿Cómo se te ha ocurrido pedirme que 
			me reúna contigo aquí? ¿No sabes que la ciudad está llena de 
			soldados?";
		], 
		reaction [;
			parent(self).remove_topic(e2_01);
		];


