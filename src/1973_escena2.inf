

!!==============================================================================
!!
!!	PROYECTO 1973
!!	Escena 2: Café Blanca Florida
!!
!!==============================================================================
!!
!!	File:			1973_escena2.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/02/04
!!
!!------------------------------------------------------------------------------
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!	This file is part of PROYECTO 1973.
!!
!!	PROYECTO 1973 is free software: you can redistribute it and/or modify 
!!	it under the terms of the GNU General Public License as published by 
!!	the Free Software Foundation, either version 3 of the License, or 
!!	(at your option) any later version.
!!
!!	PROYECTO 1973 is distributed in the hope that it will be useful, 
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of 
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License 
!!	along with PROYECTO 1973. If not, see <http://www.gnu.org/licenses/>.
!!
!!------------------------------------------------------------------------------
!!
!!	# TODO
!!
!!	<>	Revisar sistema de ayuda cuando no se empieza desde la escena 1.
!!	<>	Inicializaciones de la escena. Gabriel tiene que tener la ropa puesta 
!!		pero los contenidos de los bolsillos no son los mismos que al final de 
!!		la escena 1.
!!	<>	Muestras de duda antes de coger el billete y el revólver y pasar a la 
!!		escena 3.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	CAFE: Café Blanca Florida
!!------------------------------------------------------------------------------

Room	CAFE "Café Blanca Florida"
 with	name	'cafe' 'blanca' 'florida' 'cafeteria' 'bar' 'local' 'taberna' 
				'meson' 'tasca' 'establecimiento',
		initial [; if (self hasnt visited) {
			!! Inicializaciones de la escena -----------------------------------
			!! TODO - Resto de inicializaciones
			SetGrammaticalInflection(THIRD_PERSON_PAST);
			!! Inicializaciones multimedia -------------------------------------
			#Ifdef TARGET_GLULX;
			Damusix.AsignarCanal(BAR_ENVIRONMENT, 0, 50, -1);
!! TODO
!			Damusix.Tocar(BAR_ENVIRONMENT);
			#Endif; ! TARGET_GLULX;
			!! Introducción ----------------------------------------------------
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 2, (self has visited));
		],
		w_to	[; return self.out_to(); ],
		out_to	"No podía irse aún. No sin hablar antes con Pablo.",
		cant_go "Por ahí no podía ir a ningún lado.";

!!------------------------------------------------------------------------------
!! Objetos con rutinas react_before:

Atrezzo	silla_cafe "silla" CAFE
 class	SmartEnterable
 with	name_m	'asiento' 'taburete', 
		name_f	'banqueta' 'silla', 
		gender	G_FEMENINO, 
		!! TODO - adjectives
		clarification "sentado a una de las mesas", 
		reach_objects mesa_cafe Pablo,
		drop_on_object mesa_cafe,
		description "Era una silla normal y corriente.", 
		react_before [;
			TalkTo:
				if ((parent(player) ~= self) && (noun == Pablo)
						&& (conversacion_Pablo_01.counter == 0)) {
					move player to self;
					print "Atravesó el local hasta sentarse frente a él.^";
					new_line;
					return false;
				}
			if (self.SmartEnterable::react_before()) return true;
			Examine, Look:
				if (parent(player) == self && noun == telefono)
					"Un teléfono público negro. Desde la silla no podía ver 
					mucho más.";
		],
		before [;
			Pull, Push, PushDir, Take, Turn:
				"Ya estaba", (n) self, " muy bien donde estaba", (n) self, 
				". No había ningún motivo para andar moviéndol", (o) self, ".";
			Sit:
				<<Enter self>>;
			StandUp:
				<<Exit self>>;
		],
		after [;
			Enter:
				"Se sentó en ", (the) self, ", frente a Pablo.";
			Exit:
				"Se levantó de ", (the) self, ".";
		],
 has	female supporter transparent;

!!------------------------------------------------------------------------------
!! Atrezzo:

Atrezzo	mesa_cafe "mesa" CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble',
		name_f	'mesa',
		name_mp	'muebles',
		gender	G_FEMENINO,
		adjectives
			'corriente' 'funcional' 'normal' 'ordinaria' 'ordinario' 'sencilla' 
			'sencillo' 'simple',
		description [;
			if (child(self)) {
				print "Encima había ";
				WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT);
			} else print "Una simple mesa. No tenía nada de especial";
			".";
		],
		before [;
			Climb, Enter, Sit:
				"No había ningún motivo para subirse ahí.";
			Pull, Push, PushDir, Take, Turn:
				print_ret (The) self, " ya estaba", (n) self, " bien donde 
				estaba", (n) self, ". No había motivos para andar moviéndol", 
				(o) self, ".";
			Search:
				if (child(self) ofclass Atrezzo)
					"Sobre la mesa sólo estaba la bebida de Pablo.";
		],
 has	female supporter transparent;

Atrezzo	-> -> "bebida de Pablo"
 with	name_m	'tinto' 'vaso' 'vino', 
		name_f	'bebida',
		gender	G_FEMENINO, 
		adjectives
			'alcohol' 'alcoholica' 'color' 'oscura' 'oscuro' 'pablo' 'roja' 
			'rojo' 'sangre',
		description
			"Pablo bebía un vaso de vino tinto. Oscuro como la sangre.",
		before [;
			Examine:
				return false;
			Drink, Pull, Push, PushDir, Take, Touch, Turn:
				"Esa era la bebida de Pablo. No se la iba a quitar.";
			default:
				"Era un simple vaso de vino sin nada de especial. No hacía 
				falta preocuparse por él.";
		],
 has	female;

Atrezzo	"mesas y sillas del café" CAFE
 with	name_m	'mobiliario' 'moblaje' 'mueble' 'taburete',
		name_mp	'asientos' 'muebles' 'taburetes',
		name_fp	'banquetas' 'mesas' 'sillas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 
			'corriente' 'corrientes' 'funcional' 'funcionales' 'normal' 
			'normales' 'ordinaria' 'ordinarias' 'ordinario' 'ordinarios' 
			'sencilla' 'sencillas' 'sencillo' 'sencillos' 'simple' 'simples',
		description
			"Había unas cuantas mesas y sillas de aspecto corriente 
			repartidas por todo el bar.",
		before [;
			Examine, Search, LookUnder:
				return false;
			Enter, Sit:
				<<Enter silla_cafe>>;
			Exit, StandUp:
				<<Exit silla_cafe>>;
			default:
				"No era necesario preocuparse por eso. Después de todo, no 
				tenía", (n) self, " nada de especial.";
		],
 has	female pluralname;

Atrezzo	barra "barra" CAFE
 with	name_m	'mobiliario' 'moblaje' 'mostrador' 'mueble',
		name_f	'barra' 'repisa',
		name_mp 'muebles',
		gender	G_FEMENINO, 
		!! TODO - adjectives
		description [;
			return O__M(self, 1);
		], 
		before [;
			Pull, Push, PushDir, Turn:
				"Le hubiese sido imposible mover eso.";
			Take:
				"Le hubiese sido imposible coger eso.";
		],
 has	female;

!!	<>	El atributo 'general' se activa después de que Mr. Panamá se haya ido.
Atrezzo	cliente "cliente" CAFE
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama', !! TODO
		description [;
			if (self hasnt general) O__M(self, 1);
			else O__M(self, 2);
			if (clientes hasnt general) {
				print " ";
				<<Listen clientes>>;
			}
			new_line;
			return true;
		],
		react_before [;
			if (noun ~= self) return false;
			Attack: <<Attack clientes second>>;
			Listen: <<Listen clientes second>>;
			Pull, Push: <<Pull clientes second>>;
			TalkTo: <<TalkTo clientes second>>;
		],
 has	male; !! TODO - animate?

Atrezzo	clientes "clientes" CAFE
 with	name_f	'clientela' 'gente' 'persona',
		name_mp	'chicos' 'clientes' 'hombres' 'mozos' 'parroquianos' 'senores',
		name_fp	'chicas' 'mozas' 'mujeres' 'parroquinas' 'personas' 'senoras',
		gender	G_MASCULINO + G_PLURAL,
		!! TODO - adjectives
		description [;
			O__M(self, 1);
			if (~~self.listen_flag) {
				print " ";
				<<Listen self>>;
			}
			new_line;
			return true;
		],
		react_before [;
			Listen:
				if (noun == nothing)
					<<Listen self>>;
		],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.listen_flag) self.listen_flag = true;
				return O__M(self, 2);
			TalkTo:
				return O__M(self, 3);
		],
 private
		!! Se activa al escuchar el objeto por primera vez.
		listen_flag false,
 has	animate male pluralname;

Atrezzo	humo "humo" CAFE
 with	name_m	'cigarrillo' 'cigarro' 'humo' 'pitillo' 'puro' 'tabaco',
		name_f	'fumarada' 'humarada',
		name_mp	'cigarrillos' 'cigarros' 'pitillos' 'puros',
		gender	G_MASCULINO, 
		adjectives
			'apestado' 'corrompido' 'hediondo' 'infecto' 'maloliente' 
			'pestilente' 'volutas',
		description [;
			return O__M(self, 1);
		], 
		react_before [;
			Smell: 
				if (noun == nothing)
					<<Smell self>>;
		], 
		before [;
			Examine:
				return false;
			Smell:
				return O__M(self, 2);
			default:
				"No hacía falta que se preocupase por eso.";
		], 
 has	male;

Atrezzo	telefono "teléfono público" CAFE
 with	name_m	'auricular' 'cajetin' 'disco' 'microfono' 'microtelefono' 
				'teclado' 'telef' 'telefono' 'telf' 'visor',
		name_f	'cabina' 'inscripcion' 'placa' 'ruleta',
		gender	G_MASCULINO,
		adjectives !! TODO
			'aparato' 'ficha' 'fichas' 'negra' 'negro' 'numerico' 'publica' 
			'publico' 'telefonica' 'telefonico',
		description [;
			return O__M(self, 1, self.tokens);
		],
		before [;
			Use, TalkOn:
				!! TODO - Contemplar opción de llamar por telf.
				"No tenía a nadie a quien llamar por teléfono en aquel 
				momento.";
		],
 private
		tokens 0,
 has	male;

Atrezzo	ventilador "ventilador" CAFE
 with	name_m	'ventilador', 
		name_f	'aspa', 
		name_fp	'aspas', 
		gender	G_FEMENINO, 
		adjectives 'encendido' 'movimiento' 'negra' 'negras' 'negro' 'techo', 
		description [; return O__M(self, 1); ],
		before [;
			Examine:
				return false;
			Listen:
				return O__M(self, 2);
			default:
				return O__M(self, 3);
		],
 has	male;

!!------------------------------------------------------------------------------
!! Furniture:

!!------------------------------------------------------------------------------
!! Items:

Item	cerveza "cerveza de Gabriel" LIMBO
 with	name_m	'botellin', 
		name_f	'bebida' 'botella' 'cerveza', 
		gender	G_FEMENINO,
		adjectives 'alcohol' 'alcoholica' 'gabriel',
		description [;
			print "Un botellín de cerveza";
			if (self.counter == 0) " vacío.";
			if (self.counter == 1) " al que apenas le quedaba un trago.";
			if (self.counter <  4) " medio vacío.";
			if (self.counter < 10) " que Gabriel tenía a medias.";
			" sin empezar.";
		],
		before [;
			Drink:
				if (self.counter == 0) "Ya estaba vacío.";
				self.counter--;
				if (self.counter == 0) "Apuró el último trago del botellín.";
				"Gabriel dio un trago de su botellín de cerveza.";
		],
 private
		counter 0, ! Indica la cantidad de bebida que queda
 has	female;

Item	vino "vino de Pablo" LIMBO
 with	name_m	'tinto' 'vaso' 'vino',
		name_f	'bebida',
		gender	G_MASCULINO,
		adjectives 'alcohol' 'alcoholica' 'pablo',
		description "A Pablo siempre le había gustado el tinto.",
		before [;
			Examine:
				return false;
			Drink, Pull, Push, PushDir, Take, Touch, Turn:
				"Esa era la bebida de Pablo.";
			default:
				"No tenía nada especial por lo que mereciese perder el tiempo 
				con eso.";
		],
 has	male;

Item	billete_cafe "billete" LIMBO
 with	name_m	'billete' 'papel' 'pase', 
		gender	G_MASCULINO, 
		adjectives 'antonio' 'barcaza' 'pedazo' 'san' 'trozo',
 has	male;

!!------------------------------------------------------------------------------
!! Camarera:

NPC		camarera "camarera" CAFE
 with	name_m	'camarero' 'despensero',
		name_f	'camarera' 'chica' 'dama' 'despensera' 'joven' 'moza' 
				'muchacha' 'mujer' 'persona' 'senora' 'senorita',
		gender	G_FEMENINO,
		!! TODO - adjectives
		description [;
			return O__M(self, 1);
		],
		before [;
			Attack, Pull, Push:
				"No era el momento ni el lugar para comenzar una pelea.";
			TalkTo:
				ConversationManager.start(conversacion_camarera);
				return true;
		],
		conversation_flag false,
 has	female concealed;

Conversation conversacion_camarera
 with	topics camarera_tema_02, 
		temporal_topics camarera_tema_01,
		talker camarera,
		initial_message [;
			print "Gabriel se acercó hasta la barra.^";
			new_line;
			print (string) random (
				"---¿Quieres alguna cosa?",
				"---¿Te sirvo alguna cosa?",
				"---¿Qué vas a tomar?",
				"---¿Qué tomas?",
				"---¿Qué te sirvo?",
			);
			new_line;
			return true;
		],
		inter_message
			"La camarera observaba a Gabriel desde el otro lado de la barra.",
		final_message
			"No llegó a cruzar ninguna otra palabra con ella. Era con Pablo con 
			quien necesitaba hablar.";

ConversationTopic camarera_tema_01
 class	TextSequence
 with	entry [;
			print "no pedir ", (strong) "nada";
		],
		keys !! TODO
			'nada' 'pedir' 'pide',
		reply [;
			give self ~visited;
			"No quería nada en realidad. Dió las gracias a la mujer y se alejó 
			de nuevo de la barra.";
		],
		reaction [;
			ConversationManager.end();
		];

ConversationTopic camarera_tema_02
 class	TextSequence
 with	entry [;
			print "pedir una ", (strong) "bebida para Gabriel";
		],
		keys !! TODO
			'pedir' 'pide' 'bebida' 'gabriel',
		reply [;
			"La camarera le sirvió una cerveza.";
		],
		reaction [;
		];

ConversationTopic camarera_tema_03
 class	TextSequence
 with	description
		[;
			if (cerveza in LIMBO)
				"---Ponme una cerveza y un tinto, por favor.";
			"---Ponme un tinto, por favor.";
		]
			"---Claro."
		[ x;
			if (cerveza in LIMBO) x = true;
			print "La camarera le sirvió";
			if (x) print " las bebidas"; else print " la bebida";
			print " y después Gabriel volvió hasta la mesa con";
			if (x) " ellas."; else " ella.";
		]
			"---Ahí tienes, Pablo."
			"---Gracias, chico.",
		entry [;
			if (cerveza in LIMBO) print "pedir ", (emph) "bebidas";
			else print "pedir una ", (emph) "bebida para Pablo";
		],
		keys !! TODO
			'pedir' 'pide' 'bebidas' 'bebida' 'pablo',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic camarera_tema_04
 class	TextSequence
 with	description
			"---Perdona, ¿para usar el teléfono?"
			"---Si, te vendo yo las fichas, si quieres."
			"---Bien. ¿Puedes darme alguna, por favor?"
			"La camarera le puso unas fichas sobre el mostrador."
			"---Ahí tienes.",
		entry [;
			print "pedir ", (emph) " fichas para el teléfono";
		],
		keys
			'pedir' 'pide' 'ficha' 'fichas' 'telefono' 'telef' 'telf' 'tlf',
		reply [;
			self.display();
		],
		reaction [;
		];

!!------------------------------------------------------------------------------
!! Mr. Panamá:

NPC		MrPanama "hombre del sombrero" LIMBO
 with	name_m 'cliente' 'desconocido' 'extrano' 'hombre' 'parroquiano' 'senor',
		gender G_MASCULINO,
		adjectives 'sombrero' 'panama', !! TODO
		description [;
			if (~~self.telephone_flag) return O__M(self, 1);
			else return O__M(self, 2);
		],
		react_before [;
			Look:
				!! Si el jugador intenta mirar la localidad cuando está activo 
				!! alguno de los indicadores de movimiento de Mr. Panamá, se 
				!! lanza la acción examinar a Mr. Panamá en su lugar para que 
				!! se imprima el texto del movimiento.
				if (noun == nothing)
					if (self.movement_1_flag || self.movement_2_flag 
						|| self.movement_3_flag)
						<<Examine self>>;
			Examine, Search:
				!! Si el jugador intenta mirar al teléfono mientras Mr. Panamá 
				!! está hablando por él, se imprime la descripción del hombre.
				if (noun == telefono && self.telephone_flag)
					<<Examine self>>;
				!! Si el jugador intenta examinar a Mr. Panamá o a la clientela 
				!! cuando está activo alguno de los indicadores de movimiento, 
				!! se imprime el texto del movimiento en lugar de la 
				!! descripción del hombre o de los clientes.
				if (noun == self or clientes) {
					if (self.movement_1_flag) {
						self.movement_1_flag = false;
						return O__M(self, 3);
					}
					if (self.movement_2_flag) {
						self.movement_2_flag = false;
						return O__M(self, 4);
					}
					if (self.movement_3_flag) {
						self.movement_3_flag = false;
						return O__M(self, 5);
					}
				}
			!! TODO - Comportamiento de TalkTo
		],
		before [;
			Attack, Pull, Push, PushDir:
				!! TODO - Cambiar mensaje
				"No era el momento ni el lugar para empezar una pelea.";
			Listen:
				if (~~self.telephone_flag) return O__M(self, 6);
				if (self.listen_flag) return O__M(self, 8);
				self.listen_flag = true;
				return O__M(self, 7);
			TalkTo:
				return O__M(self, 9);
		],
		!! TODO - Gestión de los movimientos del personaje, usando daemon o 
		!! temporizadores.
 private
		listen_flag		false,
		movement_1_flag	false,
		movement_2_flag	false,
		movement_3_flag	false,
		telephone_flag	false,
 has	concealed male;

!!------------------------------------------------------------------------------
!! Pablo:

NPC		Pablo "Pablo" CAFE
 with	name_m	'pablo' 'ureta' 'amigo' 'hombre', 
		name_f	'persona', 
		gender	G_MASCULINO, 
		!! TODO: adjectives
		describe [;
			new_line;
			if (~~self.describe_flag) {
				self.describe_flag = true;
				return O__M(self, 1, (parent(player) == silla_cafe));
			}
			return O__M(self, 2, (parent(player) == silla_cafe));
		],
		description [;
			return O__M(self, 3, (parent(player) == silla_cafe));
		],
		before [;
			TalkTo:
!				if (parent(MrPanama) ~= parent(self)) {
!					move cliente to LIMBO;
!					move MrPanama to parent(self);
!					!! TODO - Activar temporizador de Mr. Panamá.
!				}
				ConversationManager.start(conversacion_Pablo_01);
				return true;
		], 
 private
		describe_flag false,	! indica si el objeto ha sido descrito
 has	male proper;

Conversation conversacion_Pablo_01
 with	talker Pablo,
		topics Pablo_tema_02 Pablo_tema_01,
		initial_message [;
			switch (conversacion_Pablo_01.counter) {
			0:	conversacion_Pablo_01.counter++;
				"---Ahí estás. Llegas tarde, chico.";
			default:
				"Pablo hizo un gesto a Gabriel con la cabeza.";
			}
		],
		inter_message
			"Pablo miraba paciente a Gabriel.",
		counter 0;

ConversationTopic Pablo_tema_01
 class	TextSequence
 with	description
			"---Si, lo sé. Lo siento. He tenido que dar un rodeo enorme para 
			llegar hasta aquí. Hay soldados por toda la ciudad."
			"---¡No me digas! Claro que hay soldados, carajo... Ya no hay sitio 
			seguro en toda la capital. Sin Allende, todo está perdido. Es no 
			más cuestión de tiempo que nos agarren esos mamones con pistola. 
			---Terminó lo que le quedaba en el vaso con un trago rápido y luego 
			hizo un ademán para señalar la diana del otro extremo del bar. 
			---Ven, juguemos.",
		entry [;
			print "pedir ", (strong) "disculpas";
		],
		keys
			'disculparte' 'disculpas' 'excusarte' 'excusas' 'pedir' 'pedirle' 
			'perdon' 'pide',
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_02);
		],
		subtopics Pablo_tema_03 Pablo_tema_04;

ConversationTopic Pablo_tema_02
 class	TextSequence
 with	description !! TODO
			"---¿Es que estás loco, Pablo? ¿Cómo se te ha ocurrido citarme 
			aquí justo ahora? La ciudad parece un hormiguero lleno de 
			soldados."
			"Terminó lo que le quedaba en el vaso con un trago rápido y luego 
			señaló con un ademán la diana de dardos colgada al otro extremo del 
			bar. ---Ven, juguemos.",
		entry [;
			print (strong) "llamarle loco", " por citarle allí en un momento 
			como aquel";
		],
		keys !! TODO
			'decir' 'decirle' 'desequilibrado' 'llamar' 'llamarle' 'loco',
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_01);
			!! TODO - Cambiar disposición de Pablo *
		],
		subtopics Pablo_tema_03 Pablo_tema_04;

ConversationTopic Pablo_tema_03
 class	TextSequence
 with	description
			"Pablo se hizo hasta la barra para devolver el vaso vacío y pedir 
			un juego de dardos a la camarera.
			^^
			---Has oido lo que ha ocurrido, ¿verdad? ---Hablaba mientras se 
			colocaba frente a la diana y empezaba a lanzar dardos."
			"---Dicen que lo mataron, pero era demasiado orgulloso para dejarse 
			arrastrar. Los de arriba hablan de que se dio el viaje..."
			"---¡Qué sabrán los de arriba! ---Su dardo se clavó con bastante 
			más fuerza de la necesaria. ---Ellos jamás creyeron en Salvador. Ya 
			antes le dieron la patada, ahora no iban a hacer menos. No, él no 
			huiría, lo dejó bien claro. Estoy seguro de que al final ha pagado 
			la lealtad del pueblo, pero también la deslealtad de los cabrones 
			de Unidad, que le dejaron bien en la Moneda con el culo al aire. 
			---Hizo entonces una pausa y lanzó su último dardo. ---No sé ya si 
			lo mataron o se mató él, pero estoy seguro de que..."
			"---No lo digáis..."
			!! TODO - calcular puntos de Pablo
			"---... está ya criando malvas, chico, y eso no lo vamos a 
			solucionar ni tú ni to. Estamos bien jodidos. ---Arrancó los dardos 
			de la diana y se los tendió luego a Gabriel. ---Quince y diez y 
			doce. Treinta y siente puntos. Ten. Tu turno.",
		entry [;
			print (strong) "aceptar", " partida";
		],
		keys	!! TODO
			'aceptar' 'partida' 'juego',
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_04);
		];

ConversationTopic Pablo_tema_04
 class	TextSequence
 with	description
			"---No creo que sea buena idea llamar la atención.",
		keys	!! TODO
			'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "decirle que ", (strong) "no es buena idea", " llamar la 
			atención";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_03);
		],
		subtopics Pablo_tema_05 Pablo_tema_06;

ConversationTopic Pablo_tema_05
 class	TextSequence
 with	description
			"---Vale. Echemos una partida.",
		keys	!! TODO
			'ceder' 'aceptar' 'partida' 'juego',
		entry [;
			print "ceder y ", (strong) "aceptar", " la partida";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_06);
		];

ConversationTopic Pablo_tema_06
 class	TextSequence
 with	description
			"---Tal y como están las cosas, no creo que sea buena idea llamar 
			la atención, Pablo. Nunca se sabe quién puede estar mirando.",
		keys	!! TODO
			'insistir' 'no' 'buena' 'idea' 'llamar' 'atencion',
		entry [;
			print "insistir en que ", (strong) "no era buena idea";
		],
		reply [;
			self.display();
		],
		reaction [;
			parent(self).remove_topic(Pablo_tema_05);
			!! TODO - Cambiar disposición de Pablo *
		],
		subtopics;

!! TODO - Pablo_tema_07 sólo tiene que estar disponible si el jugador ha 
!! llegado a ver a Mr. Panamá.

Conversation conversacion_Pablo_02
 with	topics Pablo_tema_10 Pablo_tema_09 Pablo_tema_08 Pablo_tema_07;

ConversationTopic Pablo_tema_07
 class	TextSequence
 with	description
			"---¿Conoces a ese hombre?",
		keys	!! TODO
			'preguntar' 'hombre' 'desconocido' 'barra' 'sombrero' 'panama',
		entry [;
			print "preguntar por ", (strong) "el hombre", "de la barra";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_08
 class	TextSequence
 with	description
			"Le tendió los dardos de nuevo a Pablo. ---¿Llegará ayuda?"
			"¡Ja!... ¿De quién, de los gringos? Esos precisamente no están de 
			nuestra parte, chico. ---Mientras hablaba lanzaba con furia contra 
			la diana. ---Aquí los únicos que te van a echar una mano son los 
			muñequitos de verde, y directa al cuello. Me temo que te tienes 
			que salvar tú sólo.",
		entry [;
			print "preguntar si pueden esperar ayuda";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'esperar' 'ayuda',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_09
 class	TextSequence
 with	description
			"---¿Y qué hay de los demás? ¿El resto de la gente de Unidad?",
		entry [;
			print "preguntar por el resto de la gente del partido";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'resto' 'gente' 'partido' 'unidad',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_10
 class	TextSequence
 with	description
			"---¿Nosotros no podríamos reorganizar al pueblo y...?"
			!! TODO - calcular puntos de Pablo
			"---Baja, baja de la nube, chico. El miedo les tiene a todos bien 
			agarrados por las pelotas, ni uno dirá nada. Y aún así, ¿qué 
			querías que hicieran?, atacarlos con tu mierda de claveles y... 
			perdona, no digo más que puras tonterías. Pero es cierto, más te 
			vale que huyas, hijo. ---Le pasó los tres dardos a Gabriel después 
			de anotar 40 puntos más.",
		entry [;
			print "preguntar por lo que podían hacer entonces";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'pueden' 'podian' 'hacer' 'entonces',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_11
 class	TextSequence
 with	description
		[;
			print "Gabriel lanzó el primero de sus dardos: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"---¿Y a dónde quieres que vaya?"
			"---No importa. A cualquier lugar, donde quieras, pero lejos de 
			aquí. Y cuanto antes."
		[;
			print "Lanzó el segundo dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo seguía hablando: ---Hay una barcaza en el puerto de San 
			Antonio que sale dentro de cinco días. Ya estás corriendo si no 
			quieres que te descubran y te den morada en la cara."
		[;
			print "Su último dardo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			"Después de dos rondas, Pablo había conseguido XX y XX: XX puntos, 
			mientras que Gabriel XX y XX: XX puntos.";
		],
		entry [;
			print "preguntar a dónde iba a huir";
		],
		keys	!! TODO
			'preguntar' 'pregunta' 'donde' 'ir' 'huir' 'escapar' 'lugar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_12
 class	TextSequence
 with	description
			"---No soy un cobarde, Pablo. No pienso huir."
			"---¡La cobardía no tiene nada que ver con esto, chico! ¡Estamos 
			hablando de tu vida! O pones pies en polvorosa o estás perdido.",
		entry [;
			print "negarte a escapar como un cobarde";
		],
		keys	!! TODO
			'negarte' 'negar' 'niegate' 'niega' 'escapar' 'huir' 'cobarde',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_13
 class	TextSequence
 with	description
			"---De ninguna manera, Pablo. Me da igual lo que digas. No me 
			pienso mover de aquí.",
		keys	!! TODO
			'insistir' 'insiste' 'ir' 'ningun' 'sitio' 'lugar' 'no' 'huir' 
			'escapar',
		entry [;
			print "insistir en que no pensaba ir a ningún sitio";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo
		];

ConversationTopic Pablo_tema_14
 class	TextSequence
 with	description
			"---¿De verdad no conoces a ese hombre del sombrero?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'hombre' 'desconocido' 
			'sombrero' 'panama',
		entry [;
			print "preguntar por el hombre";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_15
 class	TextSequence
 with	description
			"---¿Vendréis conmigo, Pablo?"
			"Entonces él clavó la vista en la diana, pero en realidad miraba a 
			algún lugar oculto a los ojos de Gabriel. Oculto a otros ojos 
			cualesquiera salvo a los suyos. Lanzó sus tres dardos 
			mecánicamente, uno tras otro, por 15, 20 y 18 puntos, mientras 
			sobre sus cabezas las aspas del ventilador continuaban girando con 
			su zumbido monótono. Los ecos de las demás conversaciones flotaban 
			entre ellos, extrañamente lejanas. Insignificantes. Sacudió la 
			cabeza lentamente."
			"---No.",
		entry [;
			print "preguntar si le iba a acompañar";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'acompanar' 
			'acompanara',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_16
 class	TextSequence
 with	description
			"---¿Qué? Pero... ¿pero no acabáis de decir? ¿Estáis loco o es que 
			queréis quedaros a ver cómo os dan el paseíllo?"
			"---Yo ya no tengo nada que perder, chico. Mi futuro era ver éste 
			país levantado por los chilenos unidos, pero se nos adelantaron los 
			maricas del rifle y las botas, y todo se ha jodido. En realidad es 
			como si estuviese muerto. Toda la vida luchando por un fin, y ahora 
			ese fin está más lejos que nunca. ---Contó los XX puntos que había 
			conseguido antes de devolverle los dardos a Gabriel. ---Venga, 
			lanza.",
		entry [;
			print "preguntarle si es que quería que lo ", (strong) "matasen";
		],
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'queria' 'quiere' 
			'maten' 'matasen' 'morir' 'asesinen' 'asesinarle',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_17
 class	TextSequence
 with	description
			"---¿Por qué me abandonáis?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'quiere' 'queria' 
			'abandonar' 'dejarle' 'abandonarle',
		entry [;
			print "preguntarle por qué le ", (strong) "quería abandonar";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_18
 class	TextSequence
 with	description
			"---Eres un cobarde, Pablo.",
		keys	!! TODO
			'llamarle' 'llamale' 'llama' 'cobarde' 'pablo',
		entry [;
			print "llamarle ", (strong) "cobarde";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

ConversationTopic Pablo_tema_19
 class	TextSequence
 with	description
			"Gabriel guardó silencio.",
		keys	!! TODO
			'guarda' 'guardar' 'quedate' 'quedar' 'silencio',
		entry [;
			print "guardar silencio";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_20
 class	TextSequence
 with	description
			"Aún queda la esperanza de volver, Pablo..."
			"---¡Menuda elección me das, carajo! Esperanza o muerte. Ninguna de 
			las dos me dará lo que buscaba. Verás, en la vida creemos que 
			siempre podemos elegir, y que podemos ganar. Pero cuando te obligan 
			a escoger, entonces chico, entonces sólo nos queda perder. Y yo 
			prefiero perderlo ya todo, que no perder y llorar lo que me queda 
			de mísera vida. ---Hizo una leve pausa antes de añadir como con un 
			gruñido: ---Venga, chico. Lanza. Terminemos la partida."
			"Pablo había conseguido XX, XX y XX: XXX puntos, mientras que 
			Gabriel XX y XX: XX puntos. En silencio, se preparó para lanzar sus 
			últimos tres dardos:"
		[;
			print "Primero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Segundo: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
		[;
			print "Tercero: ";
			PressAnyKey();
			print LanzaDardo(), " puntos.";
		]
			"Pablo se encargó de hacer el recuento: ---Hacen en total XXX 
			puntos. Contra mis XXX, mira qué bien. Yo gano. ---La sonrisa que 
			se dibujó en su rostro no era alegre. No lo era en absoluto. 
			---Ten."
			"---¿Qué cosa me dáis?"
			"---Un premio de consolación. Es el billete para la barcaza de la 
			que te he hablado... ---miró a uno y otro lado antes de volver a 
			meterse la mano en el bolsillo. Un momento después le tendió el 
			revólver disimuladamente, junto con el billete. ---... y un arma, 
			por si las moscas. Cinco días son mucho tiempo, y Santiago se ha 
			convertido en una ciudad peligrosa."
			"---N-no. Yo... no puedo..."
			"---¡Pues claro que puedes, diantre! Trágate esos escrúpulos tuyos 
			por una vez, chico. Espero que no tengas que utilizarlo, sobre todo 
			después de ver la horrible puntería que tienes, pero en este 
			maldito país ya no se puede estar seguro de nada... además, aunque 
			no lo uses, yo me sentiré mejor si lo llevas contigo. Tómalo. 
			Tómalo, Gabriel. Por favor.",
		entry [;
			print "sugerir que quedaba la ", (strong) "esperanza de volver";
		],
		keys	!! TODO
			'sugerir' 'sugiere' 'sugierele' 'esperanza' 'volver' 'regreso' 
			'regresar',
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_21
 class	TextSequence
 with	description
			"---De verdad crees que ésta es la única opción, ¿no es cierto?",
		keys	!! TODO
			'pregunta' 'preguntar' 'preguntale' 'preguntarle' 'habia' 'hay' 
			'mas' 'otras' 'opciones' 'alternativas',
		entry [;
			print "preguntar si no había ", (strong) "más opciones";
		],
		reply [;
			self.display();
		],
		reaction [;
		];

ConversationTopic Pablo_tema_22
 class	TextSequence
 with	description
			"---Si tú no piensas huir, yo tampoco. No voy a dejarte solo, 
			Pablo.",
		keys	!! TODO
			'di' 'dile' 'decir' 'decirle' 'piensa' 'pensaba' 'ir' 'irse' 'huir' 
			'escapar' 'sin' 'pablo',
		entry [;
			print "decirle que no pensaba irse sin él";
		],
		reply [;
			self.display();
		],
		reaction [;
			!! TODO - Cambiar disposición de Pablo **
		];

!!------------------------------------------------------------------------------

!! TODO
[ LanzaDardo result;
	result = AimAndFire();
	if (result == -2)	return 10;
	if (result == 0)	return 50;
	if (result <= 1)	return 25;
	if (result <= 3)	return 10;
	if (result <= 4)	return 5;
	if (result <= 5)	return 4;
	if (result <= 6)	return 3;
	if (result <= 7)	return 2;
	if (result <= 8)	return 1;
	return 0;
];


